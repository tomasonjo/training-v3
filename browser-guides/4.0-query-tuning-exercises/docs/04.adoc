= Exercise 4
:icons: font

== Exercise 4: Reducing Cardinality with Anchor Selectivity (Preparations)

Before you begin this exercise, you should have this environment:

. Have a database that has access to the APOC library.

. If you have not populated the database yet, execute this Cypher code to populate the database:

[source,Cypher]
----
include::scripts/initialDatabase.cypher[]
----

This is what you should see in the left panel for the database:

[.thumb]
image::InitialDatabase.png[InitialDatabase,width=300]

[start=3]
. If using Neo4j Desktop, modify the virtual memory and pagecache settings have been adjusted to be (server restart required:
[square]
* dbms.memory.heap.initial_size=4G
* dbms.memory.heap.max_size=4G
* dbms.memory.pagecache.size=1G

[start=4]
. If using Neo4j Sandbox or Neo4j Aura, confirm that your settings are at least as large by running this:

[source,Cypher]
----
CALL dbms.listConfig() YIELD name, value
WHERE name STARTS WITH 'dbms.memory'
RETURN name, value
----

[start=5]
. Warm up the Page cache (`CALL apoc.warmup.run(true,true,true)`)

You are now ready to start this exercise.

== Exercise 4: Reducing Cardinality with Anchor Selectivity (Overview)

In this exercise, you will tune queries to minimize the starting number of rows in the execution plan.

In this exercise, you will:

* *Exercise 4.1*: Reduce the anchor set of nodes.
* *Exercise 4.2*: Ensure that an index is used.
* *Exercise 4.3*: Using query hints.

Go to the next page to start this exercise.

== Exercise 4.1: Reduce the anchor set of nodes (Instructions)

Here is a query that retrieves movies that an _Actor_ has acted in where the $actor parameter is 'Tom Hanks'.

[source, cypher]
----
PROFILE
MATCH (a:Actor)-[:ACTED_IN]->(m)
WHERE a.name = $actor
RETURN m.title
----

. Execute this query (twice) and note how many rows are processed.

*IMPORTANT*: For all query tuning measurements, you must [underline]#always# run the query twice. The first time ensures that the compiled query is in the Query Cache. The second time ensures that the reported metrics for the execution plan do not include the cost of compilation.

[start=2]
. What is wrong with the query? Rewrite this query to reduce the the number of rows retrieved for the anchor.

*NOTE*: Remember to run the query twice to ensure no compilation time is included in the metrics the second time it is run.

== Exercise 4.1: Reduce the anchor set of nodes (Solution)

Here is a query that retrieves movies that an _Actor_ has acted in where the $actor parameter is 'Tom Hanks'.

[source, cypher]
----
PROFILE
MATCH (a:Actor)-[:ACTED_IN]->(m)
WHERE a.name = $actor
RETURN m.title
----

. Execute this query (twice) and note how many rows are processed.

*IMPORTANT*: For all query tuning measurements, you must [underline]#always# run the query twice. The first time ensures that the compiled query is in the Query Cache. The second time ensures that the reported metrics for the execution plan do not include the cost of compilation.

Set the parameter:

[source, cypher]
----
:param actor=>'Tom Hanks'
----

Then execute the query (twice):

The result should be:

[.thumb]
image::EX4.1.png[EX4.1,width=600]

[start=2]
. What is wrong with the query? Rewrite this query to reduce the the number of rows retrieved for the anchor.

*NOTE*: Remember to run the query twice to ensure no compilation time is included in the metrics the second time it is run.

If you look at the execution plan, the first thing you should notice is that it starts with a NodeByLabelScan.
That is not good, especially since we have indexes defined for this dataset.

Here is the query where we can take advantage of the index. The index is defined for the _Person_ nodes, not the _Actor_ nodes.

[source, cypher]
----
PROFILE
MATCH (a:Person)-[:ACTED_IN]->(m)
WHERE a.name = $actor
RETURN m.title
----

The profile when executing this code should be:

[.thumb]
image::EX4.1B.png[EX4.1B,width=600]


== Exercise 4.2: Ensure that an index is used (Instructions)

. Execute this Cypher code to retrieve  the actors who have acted in a movie who's title has a string in it that can be upper, lower, or mixed case. The value of _$testString_ is "red".

[source, cypher]
----
PROFILE
MATCH (m:Movie)
WHERE toUpper(m.title) CONTAINS toUpper($testString)
WITH m
MATCH (a:Actor)-[:ACTED_IN]->(m)
return m.title, collect(a.name) as Actors
----

What do you notice in the execution plan? The value of _$testString_ could be a sequence of characters that are in any case. That is, we want to match movie titles that contain "RED", "Red", and "red"

How can you modify the graph to support this type of query that will use an index?

The only type of query that supports case-insensitive queries is a full-text schema index.

[start=2]
. Create a full-text schema index on the _Movie.title_ property.

. Modify the query to use the full-text schema index

*Hint:* Use the wildcard character in the search string to pick up any occurrence of RED, Red, red anywhere in the title.

It is now using the index. Can you make this query faster?

== Exercise 4.2: Ensure that an index is used (Solution)

. Execute this Cypher code to retrieve  the actors who have acted in a movie who's title has a string in it that can be upper, lower, or mixed case. The value of _$testString_ is "red".

[source, cypher]
----
:parameter testString=>"red'
----

[source, cypher]
----
PROFILE
MATCH (m:Movie)
WHERE toUpper(m.title) CONTAINS toUpper($testString)
WITH m
MATCH (a:Actor)-[:ACTED_IN]->(m)
return m.title, collect(a.name) as Actors
----

What do you notice in the execution plan? The value of $testString could be a sequence of characters that are in any case. That is, we want to match movie titles that contain "RED", "Red", and "red"

Here is the result:

[.thumb]
image::EX4.2.png[EX4.2,width=500]

And here is an excerpt of the results returned (66 rows):

[.thumb]
image::EX4.2A.png[EX4.2A,width=600]

How can you modify the graph to support this type of query that will use an index?

The only type of query that supports case-insensitive queries is a full-text schema index.

[start=2]
. Create a full-text schema index on the _Movie.title_ property.

Here is the code to create the index:

[source,Cypher]
----
CALL db.index.fulltext.createNodeIndex(
      'MovieTitleMixedCase',['Movie'], ['title'])
----

[start=3]
. Modify the query to use the full-text schema index

*Hint:* Use the wildcard character in the search string to pick up any occurrence of RED, Red, red anywhere in the title.

Here is the modified code:

[source, cypher]
----
PROFILE
CALL db.index.fulltext.queryNodes('MovieTitleMixedCase', '*' + $testString + '*') YIELD node
WITH node AS movie
MATCH (a:Actor)-[:ACTED_IN]->(movie)
RETURN movie.title, collect(a.name) as Actors
----

Here is the execution plan:

[.thumb]
image::EX4.2B.png[EX4.2B,width=500]

It is now using the index. Can you make this query faster?

Here is a slightly better query:

[source, cypher]
----
PROFILE
CALL db.index.fulltext.queryNodes('MovieTitleMixedCase', '*' + $testString + '*') YIELD node
WITH node AS movie
MATCH (a)-[:ACTED_IN]->(movie)
return movie.title, collect(a.name) as Actors
----

[.thumb]
image::EX4.2C.png[EX4.2C,width=500]

== Exercise 4.3: Using query hints (Instructions)

. Execute this Cypher code to retrieve the 2 actors that are up to 6 hops away from each other in the graph. In this case _$actor1_ is "Tom Cruise" and _$actor2_ is "Kevin Bacon".

[source, cypher]
----
PROFILE
MATCH path = (p1:Person)-[:ACTED_IN*6]-(p2:Person)
WHERE p1.name = $actor1
  AND p2.name = $actor2
RETURN [n IN nodes(path) | coalesce(n.title, n.name)]
----

How many indexes are used here?

[start=2]
. Modify the query to use two indexes, one for p1 and one for p2.

== Exercise 4.3: Using query hints (Solution)

. Execute this Cypher code to retrieve the 2 actors that are up to 6 hops away from each other in the graph. In this case _$actor1_ is "Tom Cruise" and _$actor2_ is "Kevin Bacon".

[source, cypher]
----
:params {actor1: 'Tom Cruise', actor2: 'Kevin Bacon'}
----

[source, cypher]
----
PROFILE
MATCH path = (p1:Person)-[:ACTED_IN*6]-(p2:Person)
WHERE p1.name = $actor1
  AND p2.name = $actor2
RETURN [n IN nodes(path) | coalesce(n.title, n.name)]
----

How many indexes are used here?

Here is the execution plan:

[.thumb]
image::EX4.3.png[EX4.3,width=500]

[start=2]
. Modify the query to use two indexes, one for p1 and one for p2.

Here is the modified code:

[source,Cypher]
----
PROFILE
MATCH path = (p1:Person)-[:ACTED_IN*6]-(p2:Person)
USING INDEX p1:Person(name)
USING INDEX p2:Person(name)
WHERE p1.name = $actor1
  AND p2.name = $actor2
RETURN [n IN nodes(path) | coalesce(n.title, n.name)]
----

Here is the execution plan:

[.thumb]
image::EX4.3A.png[EX4.3A,width=500]

== Exercise 4: Reducing Cardinality with Anchor Selectivity (Summary)

In this exercise, you modified queries to perform better by ensuring cardinality is minimized with the anchor node selection.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/05.html'>Continue to Exercise 5</a>]
endif::[]
