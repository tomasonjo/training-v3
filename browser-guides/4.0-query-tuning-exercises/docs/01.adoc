= Exercise 1
:icons: font

== Exercise 1: Answer Some Query Tuning Questions (Preparations)

Before you begin the exercises for this course, you must  have a database that has access to the APOC library.

In Neo4j Desktop you must:

. Create a Query Tuning Project you will use for this course.
. Create a local 4.x database.
. Go to the *Manage* pane for the database (click 3 dots to the right of the database name).
. In the Manage pane, select the *Plugins* tab.
. Click the install button for APOC.
. Start the database.


Next, you must populate your graph.

Execute this Cypher code to populate the database:

[source,Cypher]
----
include::scripts/initialDatabase.cypher[]
----

This is what you should see in the left panel for the database:

[.thumb]
image::InitialDatabase.png[InitialDatabase,width=300]

You are now ready to start this exercise.

== Exercise 1: Answer Some Query Tuning Questions (Overview)

In this exercise, you will gain some experience with viewing execution plans and answer some questions about the graph.

* *Exercise 1.1*: How much of the Page Cache is being used?
* *Exercise 1.2*: Set some parameters that you will be using.
* *Exercise 1.3*: How many operations are in execution plan for this first query?
* *Exercise 1.4*: Are there any eager operations in this second query?
* *Exercise 1.5*: How many unique indexes are there in this graph?
* *Exercise 1.6*: Display the data model for this graph.
* *Exercise 1.7*: Answer some questions about the DB Stats for this graph.
* *Exercise 1.8*: Is the count store used for this query?
* *Exercise 1.9*: Which runtime is used for this query?

Go to the next page to start this exercise.

== Exercise 1.1: How much of the Page Cache is being used? (Instructions)

In Neo4j Browser, write and execute the command to view information about the Page Cache.

== Exercise 1.1: How much of the Page Cache is being used? (Solution)

In Neo4j Browser, write and execute the command to view information about the Page Cache.

You should see something like this when you enter the `:sysinfo` command:

[.thumb]
image::sysinfo.png[sysinfo,width=500]

== Exercise 1.2: Set some parameters that you will be using (Instructions)

A best practice is to always use parameters in your queries.
For the queries you will be executing in this exercise, set the following parameter values:

* actorName: "Meryl Streep"
* lookup: "The"

*Hint*: Use :params, providing the map values.

== Exercise 1.2: Set some parameters that you will be using (Solution)

A best practice is to always use parameters in your queries.
For the queries you will be executing in this exercise, set the following parameter values:

* actorName: "Meryl Streep"
* lookup: "The"

*Hint*: Use :params, providing the map values.

[source, cypher]
----
:params {actorName: "Meryl Streep", lookup: "The"}
----

The result returned should be:

[.thumb]
image::params.png[params,width=500]

== Exercise 1.3: How many operations are in execution plan for this first query? (Instructions)

Produce the execution plan for this query:

[source,Cypher]
----
MATCH (m:Movie)-[:ACTED_IN]-(a:Actor)
WHERE a.name = $actorName
WITH m
MATCH (m)-[:DIRECTED]-(d:Director)
RETURN collect(d.name) AS Directors
----

How many operators are used?

== Exercise 1.3: How many operations are in execution plan for this first query? (Solution)

Produce the execution plan for this query:

[source,Cypher]
----
PROFILE
MATCH (m:Movie)-[:ACTED_IN]-(a:Actor)
WHERE a.name = $actorName
WITH m
MATCH (m)-[:DIRECTED]-(d:Director)
RETURN collect(d.name) AS Directors
----

How many operators are used?

Answer: 8

== Exercise 1.4: Are there any eager steps in this this second query? (Instructions)

Produce the execution plan for this query:

[source,Cypher]
----
MATCH (g:Genre)-[]-(m:Movie)
WHERE m.title STARTS WITH $lookup
WITH m,  collect (DISTINCT g.name) as genres
return m.title, genres
----

Are there any eager operators used?

== Exercise 1.4: Are there any eager steps in this this second query? (Solution)

Produce the execution plan for this query:

[source,Cypher]
----
PROFILE
MATCH (g:Genre)-[]-(m:Movie)
WHERE m.title STARTS WITH $lookup
WITH m,  collect (DISTINCT g.name) as genres
return m.title, genres
----

Are there any eager operators used?

Answer: yes

== Exercise 1.5: Are there any unique indexes in this graph? (Instructions)

Write and execute the Cypher statement to show the indexes and determine the number of unique indexes defined.


== Exercise 1.5: Are there any unique indexes in this graph?  (Solution)

Write and execute the Cypher statement to show the indexes and determine the number of unique indexes defined.

[source, cypher]
----
:schema
----

The result returned should be:

[.thumb]
image::indexes.png[indexes,width=900]

This graph has three unique indexes defined.

== Exercise 1.6: Display the data model for this graph (Instructions)

Write and execute the Cypher statement to show the data model for the graph.


== Exercise 1.6: Display the data model for this graph (Solution)

Write and execute the Cypher statement to show the data model for the graph.

[source, cypher]
----
CALL apoc.meta.graph()
----

The result returned should be:

[.thumb]
image::apoc.meta.graph.png[apoc.meta.graph,width=600]


== Exercise 1.7: Answer some questions about the DB Stats for this graph (Instructions)

Write and execute the Cypher statement to show the DB Stats for the graph and answer these questions:

. How many Director nodes are there?
. How many Actor nodes are there?
. How many IS_GENRE relationships are there?


== Exercise 1.7: Answer some questions about the DB Stats for this graph (Solution)

Write and execute the Cypher statement to show the DB Stats for the graph and answer these questions:

. How many Director nodes are there?  Answer: 3099
. How many Actor nodes are there?  Answer: 16,089
. How many IS_GENRE relationships are there?  Answer 17,304


[source, cypher]
----
CALL apoc.meta.stats()
----

The result returned should be:

[.thumb]
image::meta.stats.png[meta.stats,width=600]


== Exercise 1.8: Is the count store used for this query? (Instructions)

Profile this query to see to see that the count store is not used:

[source, cypher]
----
MATCH (p:Person)-[:DIRECTED]->(m:Movie)
WITH count(*) as TotalDirectorRelationships
RETURN TotalDirectorRelationships
----

Change this query so that the count store will be used.

== Exercise 1.8: Is the count store used for this query? (Solution)

Profile this query to see to see that the count store is not used:

[source, cypher]
----
PROFILE
MATCH (p:Person)-[:DIRECTED]->(m:Movie)
WITH count(*) as TotalDirectorRelationships
RETURN TotalDirectorRelationships
----

You should see this in the execution plan:

[.thumb]
image::EX1.8.png[EX1.8,width=500]

The count store is not used because we specified a label for each end of the pattern.

Change this query so that the count store will be used.

Here is a solution:

[source, cypher]
----
PROFILE
MATCH (p:Person)-[:DIRECTED]->(m)
WITH count(*) as TotalDirectorRelationships
RETURN TotalDirectorRelationships
----

You should see this in the execution plan:

[.thumb]
image::EX1.8A.png[EX1.8A,width=600]

== Exercise 1.9: Which runtime is used for this query? (Instructions)

Set these parameters:

movie1: "The Matrix"
movie2: "Top Gun"

Profile this query to see to see which runtime is used:

[source, cypher]
----
MATCH (a:Actor)
WHERE (a)-[:ACTED_IN]->(:Movie {title:$movie1})
OR (a)-[:ACTED_IN]->(:Movie {title:$movie2})
RETURN a.name
----

Change this query so that the count store will be used.

== Exercise 1.9: Which runtime is used for this query? (Solution)

Set these parameters:

movie1: "The Matrix"
movie2: "Top Gun"

[source, cypher]
----
:params {movie1: "The Matrix", movie2: "Top Gun"}
----

[.thumb]
image::EX1.9.png[EX1.9,width=600]

Profile this query to see to see that the count store is not used:

[source, cypher]
----
PROFILE
MATCH (a:Actor)
WHERE (a)-[:ACTED_IN]->(:Movie {title:$movie1})
OR (a)-[:ACTED_IN]->(:Movie {title:$movie2})
RETURN a.name
----

You should see this in the execution plan:

[.thumb]
image::EX1.9A.png[EX1.9A,width=600]

You should see here that SLOTTED is used.

Change this query so that SLOTTED will not be used.

Here is a solution:

[source, cypher]
----
PROFILE
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.title = $movie1 OR m.title = $movie2
RETURN  a.name
----

You should see this in the execution plan:

[.thumb]
image::EX1.9B.png[EX1.9B,width=600]

== Exercise 1: Answer Some Query Tuning Question (Taking it Further)

Try executing `PROFILE` statements in cypher-shell to get used to reading the execution plan in a tabular format.

Profile this query to see to see that the count store is not used:

[source, cypher]
----
PROFILE
MATCH (p:Person)-[:DIRECTED]->(m:Movie)
WITH count(*) as TotalDirectorRelationships
RETURN TotalDirectorRelationships
----

Here is what you should see in your execution plan:

[.thumb]
image::NoCountStore.png[NoCountStore,width=600]

Change this query so that the count store will be used.

[source, cypher]
----
PROFILE
MATCH (p:Person)-[:DIRECTED]->()
WITH count(*) as TotalDirectorRelationships
RETURN TotalDirectorRelationships
----

Here is what you should see in your execution plan:

[.thumb]
image::CountStore.png[CountStore,width=600]

== Exercise 1: Answer Some Query Tuning Question (Summary)


In this exercise, you executed some commands and Cypher statements to familiarize yourself with the graph you will be working with and also the viewing of execution plans.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/02.html'>Continue to Exercise 2</a>]
endif::[]
