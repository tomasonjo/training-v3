= Refactoring Graphs
:slug: 04-igdm-40-refactoring-graphs
:doctype: book
:toc: left
:toclevels: 4
:imagesdir: ../images
:module-next-title: Summary

== About this module

At the end of this module, you should be able to:
[square]
* Create constraints that help with performance of node creation and queries.
* Determine if a query can be improved.
* Write Cypher code to refactor the data model in the graph.
* Create indexes that will help with query performance.
* Refactor a graph by creating intermediate nodes.
* Refactor a graph by specializing relationships.
* Perform batch processing to refactor a large graph.

[.half-row]
== Evolving the model

[.notes]
--
After profiling queries and determining that they are not performing optimally, you should rethink your graph data model.
You do so by examining how queries perform, especially with data at scale.

In the previous lesson, we saw that the query to retrieve the airports and flight information for flight number 1016 for airline WN ended up making two passes through the graph to perform the query.
Our small database has only 100 flights in it, but it required over 4,000 db hits to perform the query.
--

[.small]
--
To solve this problem, we introduce the intermediate node, Flight that is based upon the properties of the _CONNECTED_TO_ relationship.

Here is how the model will evolve:
--

image::Refactor1.png[Refactor1,width=1100,align=center]

== Refactoring steps

[.notes]
--
In most cases, you refactor the the implementation of the graph data model with these steps:
--

. Create constraints as needed.
. Execute the refactor:
.. MATCH the data you wish to move.
.. Create new nodes.
.. Create new relationships.
. Create indexes as needed.
. PROFILE all queries against the new model.

[NOTE]
If the new model performs well for [.underline]#all# queries, delete the old model.
Otherwise, leave both in place.

=== Refactor details

[.notes]
--
Here are the details that show the _CONNECTED_TO_ relationship properties and how they will be refactored as the Flight properties.
--

image::Refactor2.png[Refactor2,width=800,align=center]

[.notes]
--
Notice that we will be adding a unique identifier for a _Flight_ node, _flightId_.
--

=== Unique FlightId

[.notes]
--
Using a unique identifier for a flight enables us to quickly find a flight by its ID during a query.
We create a uniqueness constraint for that property.

The flight number for a particular airline may not be unique, especially if we take into account the origin and destination airports.
That is, the same flight number may be used for different from and to airports.
In addition, we want to uniquely identify the date for the flight.
--

The _Flight.flightId_ property will contain the following data which will be unique for all _Flight_ nodes:

[square]
* airline
* flightNumber
* code for the origin Airport
* code for the destination Airport
* date

== Creating the constraint

Before we perform the refactoring of the existing graph, we add the uniqueness constraint:

[source,cypher]
----
CREATE CONSTRAINT Flight_flightId_constraint ON (f:Flight)
       ASSERT f.flightId IS UNIQUE
----

== Refactoring the graph

Here is the code to refactor the graph:

[source,cypher]
----
MATCH (origin:Airport)-[connection:CONNECTED_TO]->(destination:Airport)
MERGE (newFlight:Flight {flightId: connection.airline + connection.flightNumber +
       '_' + connection.date +  '_' + origin.code + '_' + destination.code })
ON CREATE SET newFlight.date = connection.date,
              newFlight.airline = connection.airline,
              newFlight.number = connection.flightNumber,
              newFlight.departure = connection.departure,
              newFlight.arrival = connection.arrival
MERGE (origin)<-[:ORIGINATES_FROM]-(newFlight)
MERGE (newFlight)-[:LANDS_IN]->(destination)
----

[.notes]
--
This code looks scary at first, but it relies entirely on Cypher code you already know well.

It follows our refactoring steps:

[square]
. Use `MATCH` to fetch the data we wish to move.  We are looking to move the _CONNECTED_TO_ relationship data, so we create a query that will fetch every such relationship.
. Use `MERGE` to create the new _Flight_ nodes.  As is always the best practice with `MERGE`, we only include the unique identifier, _flightId_ in the `MERGE` statement, where that property is a concatenation of many individually-non-unique things.  All other _Flight_ properties are set in a subsequent `ON CREATE SET` clause, which maps 1:1 from the CONNECTED_TO properties.
. Use `MERGE` to create the relationships connecting the new nodes.  We need to do this twice, since Flights have both an ORIGINATES_FROM and LANDS_IN relationship.
--

== Index for query performance

[.notes]
--
A very common qualifier for a query is the flight number. We want lookups of a _Flight_ by its number to be fast.
A flight number is not unique, but we want to be able to look up all flights with that number quickly.
--

image::Refactor2.png[Refactor2,width=800,align=center]

[.notes]
--
You create the indexes after your have executed the refactoring code.
This is because index maintenance is expensive and you do not want a large refactoring to take a long time to execute.
--

== Creating the index

[.notes]
--
Here is the code to create the index on the flight number property:
--

[source.center, cypher]
----
CREATE INDEX Flight_number_index FOR (f:Flight) ON (f.number)
----

== Profiling the query

[.notes]
--
After you have refactored the graph, you then profile the query that you are hoping to improve.
--
[small]
--
Recall that the query is: What are the airports and flight information for flight number 1016 for airline WN?

Here is the original query:
--
[source, cypher]
----
PROFILE
MATCH  (origin:Airport)-[connection:CONNECTED_TO]->(destination:Airport)
WHERE connection.airline = 'WN' AND connection.flightNumber = '1016'
RETURN origin.code, destination.code, connection.date, connection.departure, connection.arrival
----

[small]
--
We must change the query to work with the new model as follows:
--
[source, cypher]
----
PROFILE
MATCH (origin)<-[:ORIGINATES_FROM]-(flight:Flight)-
      [:LANDS_IN]->(destination)
WHERE flight.airline = 'WN' AND
      flight.number = '1016' RETURN origin, destination, flight
----

=== Result of the profile

ifndef::env-slides[]
image::ProfileAfterRefactor.png[ProfileAfterRefactor,width=300,align=center]
endif::[]

ifdef::env-slides[]
image::ProfileAfterRefactor.png[ProfileAfterRefactor,width=120,align=center]
endif::[]

[.notes]
--
Here we see that we improved the query to require only 34 db hits.
Notice also that the index is being used at the beginning of the query execution plan to anchor the query.
It is much better than doing a NodeByLabelScan.
--

[.half-row]
== Cleaning up the graph

[.notes]
--
If you are satisfied that the new model performs best for your queries, you can then clean up the graph to remove the _CONNECTED_TO_ relationships.
Relationships, especially those with properties take up unnecessary space in the graph.
--

image::DeleteConnected_to.png[DeleteConnected_to,width=1000,align=center]

[source, cypher]
----
MATCH ()-[connection:CONNECTED_TO]->()
DELETE connection
----


[.student-exercise]
== Exercise 4: Creating Flight nodes from CONNECTED_TO relationships

[.small]
--
In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-neo4j-modeling-exercises]

and follow the instructions for Exercise 4.

[NOTE]
This exercise has 7 steps.
Estimated time to complete: 30 minutes.
--

== Adding another domain question

[small]
--
We need to add another question for our application:

As a frequent traveller I want to find flights from <origin> to <destination> on <date> so that I can book my business flight.

For example:

Find all the flights going from Los Angeles (LAS) to Chicago Midway International (MDW) on the 3rd January, 2019.
--

ifdef::env-slides[]
[.notes]
--
Whiteboard what the query might be with the students.
--
endif::[]

=== Implementing the query
[.notes]
--
Here is the query:
--

[source,cypher]
----
MATCH (origin:Airport {code: 'LAS'})
    <-[:ORIGINATES_FROM]-(flight:Flight)-[:LANDS_IN]->
    (destination:Airport {code: 'MDW'})
WHERE flight.date = '2019-1-3'
RETURN origin, destination, flight
----
[small]
--
[NOTE]
You will work with this query in the next exercise where you have loaded 10k nodes into the graph.
--

ifdef::env-slides[]
[.notes]
--
Before you execute the query, show the students that you now have 10K flight nodes in the database.
You should have 3 indexes: :Flight(number), :Flight(id), and :Airport(code).
--
endif::[]

[.one-third-two-thirds-column]
=== Profiling the query

Here is the result of the profile on a graph that contains 10k nodes.

image::ProfileBeforeRefactor4.png[ProfileBeforeRefactor4,width=400,align=center]

[.notes]
--
This query returns 12 flights.

5532 db hits which needs improvement. Adding an index to _Flight_ on date does not improve the query.

Some stats about this graph:

[square]
* 64 airports
* 10000 flights
* 661 flights that land in MDW; 377 on 2019-1-3
* 1624 flights that originate from LAS; 426 on 2019-1-3
* 2367 flights on 2019-1-3

Searching for flights by day is a problem.

The query starts by using an index to find MDW but then has to traverse all incoming _LANDS_IN_ relationships and check the _Flight.date_ property on the other side.
The more flights an airport has the more we will have to scan through, and since we are only working with 10,000 flights we should probably find a better way to model our data before importing any more data.

This is an opportunity to change the model.
--

[.five-sixths-one-sixth-row]
== Performing another refactor

[.notes]
--
We want to introduce _AirportDay_ nodes so that we do not have to scan through all the flights going from an airport when we are only interested in a subset of them.

This is an instance where we do not want to remove the relationships between airports and flights because we need them for our first query "What are the airports and flight information for flight number 1016 for airline WN?".
--

image::Refactor4.png[Refactor4,width=1000,align=center]

[small]
--
[NOTE]
You only pull out a node if you are going to query through it, otherwise a property will suffice.
--

[.notes]
--
You should not be too aggressive with creating nodes from other nodes.
If you pull out every single property and create nodes then you end up with an RDF model and lose the benefit of the property graph.
--

[.half-row]
=== Refactor details

image::Refactor4Details.png[Refactor4Details,width=600,align=center]

[.notes]
--
In this case we are adding the _AirportDay_ node that will have date information.
That way we do not have to go through the _Flight_ nodes to find a date.
Just like the _Flight_ nodes, it will have a unique ID, _AirportDay.airportDayId_ so that it can used it in the query.
--

[source, cypher]
----
CREATE CONSTRAINT AirportDay_airportDayId_constraint ON (a:AirportDay)
       ASSERT a.airportDayId IS UNIQUE
----

ifdef::env-slides[]
[.notes]
--
On the whiteboard, come up with the Cypher code to:
--
endif::[]

[.notes]
--
Then, you will want to:

[square]
* `MATCH` the data you want to move.
* Create the new _AirportDay_ nodes.
* Connect the new nodes to the existing graph.
--

== Refactor implementation

[source, cypher]
----
MATCH (origin:Airport)<-[:ORIGINATES_FROM]-(flight:Flight)-
      [:LANDS_IN]->(destination:Airport)
MERGE (originAirportDay:AirportDay {airportDayId: origin.code + '_' + flight.date})
SET originAirportDay.date = flight.date
MERGE (destinationAirportDay:AirportDay
      {airportDayId: destination.code + '_' + flight.date})
SET destinationAirportDay.date = flight.date
MERGE (origin)-[:HAS_DAY]->(originAirportDay)
MERGE (flight)-[:ORIGINATES_FROM]->(originAirportDay)
MERGE (flight)-[:LANDS_IN]->(destinationAirportDay)
MERGE (destination)-[:HAS_DAY]->(destinationAirportDay)
----

[.notes]
--
`MERGE` enables us to add a single _AirportDay_ node per the  _airportDayId_ value and also ensure that only one relationship is created between a _Flight_ and an _AirportDay_ node.
--

[.two-thirds-one-third-column]
=== Profile our first query

[.notes]
--
After a refactor, you should check that all queries perform OK. Here is our first query: What are the airports and flight information for flight number 1016 for airline WN?
--

[.is-two-thirds.left]
--
[source,cypher]
----
PROFILE
MATCH (origin)<-[:ORIGINATES_FROM]-(flight:Flight)-
      [:LANDS_IN]->(destination)
WHERE flight.airline = 'WN' AND
      flight.number = '1016' RETURN origin, destination, flight
----
--

////
ifdef::env-slides[]
[is-one-third.right]
--
image::FirstQueryAfterRefactor4.png[FirstQueryAfterRefactor4,width=200,align=center]
--
endif::[]

ifndef::env-slides[]
////
image::FirstQueryAfterRefactor4.png[FirstQueryAfterRefactor4,width=400,align=center]
//endif::[]

[.notes]
--
This query previously had 34 db hits, but now has 181. But, we added another 10K nodes to the graph so this is a reasonable outcome.
--

[.two-thirds-one-third-column]
=== Profile our original second query

[.notes]
--
Then we want to profile our second query after the refactor: Find all the flights going from Los Angeles (LAS) to Chicago Midway International (MDW) on the 3rd January, 2019.
--

[.is-two-thirds.left]
--
[source,cypher]
----
PROFILE MATCH (origin:Airport {code: 'LAS'})
    <-[:ORIGINATES_FROM]-(flight:Flight)-
    [:LANDS_IN]->
    (destination:Airport {code: 'MDW'})
WHERE flight.date = '2019-1-3'
RETURN origin, destination, flight
----
--

ifdef::env-slides[]
[.is-one-third.right]
--
image::OriginalSecondQueryAfterRefactor4.png[OriginalSecondQueryAfterRefactor4,width=160,align=center]
--
endif::[]

ifndef::env-slides[]
image::OriginalSecondQueryAfterRefactor4.png[OriginalSecondQueryAfterRefactor4,width=400,align=center]
endif::[]

[.notes]
--
Here we see that the query has 5982 db hits, which is worse than the 5532 we had earlier before the refactor.
This is because we need to incorporate the new model into the query.
--

[.two-thirds-one-third-column]
=== Profile our revised second query

[.notes]
--
Here is a rewrite of the second query: Find all the flights going from Los Angeles (LAS) to Chicago Midway International (MDW) on the 3rd January, 2019.
Due to the change in the model, we must rewrite the  query as:
--

[.is-two-thirds.left]
--
[source,cypher]
----
PROFILE MATCH (origin:Airport {code: 'LAS'})-
    [:HAS_DAY]->(:AirportDay {date: '2019-1-3'})<-
    [:ORIGINATES_FROM]-(flight:Flight),
    (flight)-[:LANDS_IN]->
    (:AirportDay {date: '2019-1-3'})<-
    [:HAS_DAY]-(destination:Airport {code: 'MDW'})
RETURN origin, destination, flight
----
--

ifndef::env-slides[]
image::SecondQueryAfterRefactor4.png[SecondQueryAfterRefactor4,width=400,align=center]
endif::[]

ifdef::env-slides[]
[.is-one-third.right]
--
image::SecondQueryAfterRefactor4.png[SecondQueryAfterRefactor4,width=160,align=center]
--
endif::[]

[.notes]
--
Here we see that the query has 4271 db hits, which is better than the 5532 we had earlier before the refactor.
As the number of nodes and relationships grows in the graph, these performance differences will be significant.

What we have learned is that we have to change the model [underline]#and# the query.
--

[.student-exercise]
== Exercise 5: Creating the AirportDay node from the Airport and Flight nodes

[.small]
--
In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-neo4j-modeling-exercises]

and follow the instructions for Exercise 5.

[NOTE]
This exercise has 7 steps.
Estimated time to complete: 30 minutes.
--

[.half-column]
== More questions for the model?

[small]
--
We now have a model that is performs well for these questions:

. What are the airports and flight information for flight number xx for airline yy?
. Find all the flights going from xx to yy on the date zz.

What if we added this question: Which airport has the most incoming flights?
--

image::FinalModel.png[FinalModel,width=600,align=center]

[.notes]
--
Our latest model serves our two questions very well.
However, it does not do a good job with the new question.
That is, we must leave the _ORIGINATES_FROM_ and _LANDS_IN_ relationships between _Airport_ and _Flight_ nodes in the graph.
In this case, we  leave both models in place, and use each one for the questions it is suited to.
--

[.two-thirds-one-third-column]
== Another question for the model

[.is-two-thirds.left]
--
[.statement]
Suppose we added this question: What are the flights from LAS that arrive at MDW on 2019-1-3?
--

[.notes]
--
To answer this question with the current model our query would be:
--

[.is-two-thirds.left]
--
[source, cypher]
----
PROFILE
MATCH (origin:Airport {code: 'LAS'})-[:HAS_DAY]->
      (originDay:AirportDay),
      (originDay)<-[:ORIGINATES_FROM]-(flight:Flight),
      (flight)-[:LANDS_IN]->(destinationDay),
      (destinationDay:AirportDay)<-[:HAS_DAY]-
      (destination:Airport {code: 'MDW'})
WHERE originDay.date = '2019-1-3' AND
destinationDay.date = '2019-1-3'
RETURN flight.date, flight.number, flight.airline,
flight.departure, flight.arrival
ORDER BY flight.date, flight.departure
----
--

ifndef::env-slides[]
image::LASToMDWBad.png[LASToMDWBad,width=400,align=center]
endif::[]

ifdef::env-slides[]
[.is-one-third.right]
--
image::LASToMDWBad.png[LASToMDWBad,width=110,align=center]
--
endif::[]

[.notes]
--
This query requires 6137 db hits with our current graph. It needs to traverse all the _HAS_DAY_ relationships between the _Airport_ and _AirportDay_ nodes found.

Neo4j is optimized for searching by relationship types.
As we add more data, the number of _HAS_DAY_ relationships that we have to traverse increases.

If we have 10 years worth of data we have to traverse 3,650 relationships from the _Airport_ to find the _AirportDay_ that we are interested in.
--

[.one-sixth-five-sixths-row]
== Refactoring for specific relationships

[.statement]
A best practice for graph data modeling is to make relationships more specific if that will help with query performance.
Here we can modify the _HAS_DAY_ relationship to be _ON_2019-1-3_, _ON_2019_104_, etc.

image::SpecificRelationshipsModel.png[SpecificRelationshipsModel,width=600,align=center]

=== APOC to the rescue!

[small]
--
With APOC, you can create relationships based upon data in the graph.

Here is the syntax:
--

[source, cypher]
----
apoc.create.relationship(startNode(<relationship-variable>),
                         '<new-relationship-value>',
                         {<relationship-property list},
                         endNode(<relationship-variable>)
                         )
                         YIELD rel
----

[.notes]
--
Given a relationship variable between two existing nodes, this procedure enables you to create a new, custom relationship that could be based upon property values.
Calling this procedure where _rel_ is returned enables you to either return the new relationship created or return a count of the number of relationships created in the graph.
--

== Creating specialized relationships with APOC

Here is the code to transform the _HAS_DAY_ relationships to specific relationships:

[source,cypher]
----
MATCH (origin:Airport)-[hasDay:HAS_DAY]->(ad:AirportDay)
CALL apoc.create.relationship(startNode(hasDay),
                              'ON_' + ad.date,
                              {},
                              endNode(hasDay) ) YIELD rel
RETURN COUNT(*)
----

=== Refactoring result

[.statement]
Here is the result of the refactoring to create specific relationships:

image::APOCToRefactor.png[APOCToRefactor,width=900,align=center]

[.two-thirds-one-third-column]
== Does the query improve?

[.statement]
Since the model has changed, we need to rewrite the query:

[.is-three-quarters.left]
--
[source, cypher]
----
PROFILE
MATCH (origin:Airport {code: 'LAS'})-[:`ON_2019-1-3`]->
      (originDay:AirportDay),
      (originDay)<-[:ORIGINATES_FROM]-(flight:Flight),
      (flight)-[:LANDS_IN]->(destinationDay),
      (destinationDay:AirportDay)<-[:`ON_2019-1-3`]
      -(destination:Airport {code: 'MDW'})
RETURN flight.date, flight.number, flight.airline,
flight.departure, flight.arrival
ORDER BY flight.date, flight.departure
----
--

ifndef::env-slides[]
image::AfterAPOCRefactor.png[AfterAPOCRefactor,width=300,align=center]
endif::[]

ifdef::env-slides[]
[.is-one-quarter.right]
--
image::AfterAPOCRefactor.png[AfterAPOCRefactor,width=120,align=center]
--
endif::[]

[.notes]
--
This query required 4108 db hits, where previously, it required 6137 hits. You can imagine that a fully-loaded graph with years of data could be vastly improved with this type of refactoring.

Of course, you must do due diligence and ensure that all of the previous queries still perform well.
--

[.student-exercise]
== Exercise 6: Creating specific relationships

[.small]
--
In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-neo4j-modeling-exercises]

and follow the instructions for Exercise 6.

[NOTE]
This exercise has 2 steps.
Estimated time to complete: 15 minutes.
--

== Refactoring large graphs

[.notes]
--
Suppose you scale the test data in your graph, or you have a large production graph and a new question must be added that requires a change to the model.
Refactoring a large graph has its challenges due to the amount of memory required to perform the refactor.

Cypher keeps all transaction state in memory while running a query, which is fine most of the time.

When refactoring the graph, however, this state can get very large and may result in an _OutOfMemory_ exception.
--

You must adapt your heap size to match, or operate in batches.
For example increase these values for the server in the *neo4j.conf* file:

[square]
* dbms.memory.heap.initial_size=2G (default is 512m)
* dbms.memory.heap.max_size=2G (default is 1G)

== Batching the refactoring process

[.notes]
--
Here is one way that you can control how much work is done for a refactoring:
--

[small]
--
. Tag all the nodes we need to process with a temporary label (for example _Process_).

[source,cypher]
----
MATCH (f:Flight)
SET f:Process
----

[start=2]
. Iterate over a subset of nodes flagged with the temporary  label (using `LIMIT`):
.. Execute the refactoring code.
.. Remove the temporary label from the nodes.
.. Return a count of how many rows were processed in the iteration.
. Once the count reaches 0, then the refactoring is finished.

--

== Example code for a batch

[.notes]
--
Here is code that will process all nodes with the label _Process_:
--

[source,cypher]
----
MATCH (flight:Process)
WITH flight LIMIT 500

MATCH (origin:Airport)<-[:ORIGINATES_FROM]-(flight)-[:LANDS_IN]->(destination:Airport)

MERGE (originAirportDay:AirportDay {airportDayId: origin.code + "_" + flight.date})
ON CREATE SET originAirportDay.date = flight.date

MERGE (destinationAirportDay:AirportDay {airportDayId: destination.code + "_" + flight.date})
ON CREATE SET destinationAirportDay.date = flight.date

MERGE (origin)-[:HAS_DAY]->(originAirportDay)
MERGE (originAirportDay)<-[:ORIGINATES_FROM]-(flight)
MERGE (flight)-[:LANDS_IN]-(destinationAirportDay)
MERGE (destination)-[:HAS_DAY]->(destinationAirportDay)

REMOVE flight:Process
RETURN count(*)
----

[.notes]
--
You have previously used this refactoring code to create the _AirportDay_ nodes and their relationships to _Airport_  and _Flight_ nodes.

This is a variation of the code we executed previously to create the _AirportDay_ nodes from _Flight_ nodes.
The highlighted areas will be part of the iteration where we do batches of 500 flights at a time and once the creation of the _AirportDay_ node is completed, we remove the _Process_ label from the _Flight_ node.
--

=== Using apoc.periodoc.commit

image::APOCPeriodicCommit.png[APOCPeriodicCommit,width=800,align=center]

[.notes]
--
APOC currently has 9 procedures that can help you with batching.
We will focus on using _apoc.periodic.commit()_.
--

== Batching with APOC


[source,cypher]
----
CALL apoc.periodic.commit('
MATCH (flight:Process)
WITH flight LIMIT $limit

MATCH (origin:Airport)<-[:ORIGINATES_FROM]-(flight)-[:LANDS_IN]->(destination:Airport)

MERGE (originAirportDay:AirportDay {airportDayId: origin.code + "_" + flight.date})
ON CREATE SET originAirportDay.date = flight.date

MERGE (destinationAirportDay:AirportDay {airportDayId: destination.code + "_" + flight.date})
ON CREATE SET destinationAirportDay.date = flight.date

MERGE (origin)-[:HAS_DAY]->(originAirportDay)
MERGE (originAirportDay)<-[:ORIGINATES_FROM]-(flight)
MERGE (flight)-[:LANDS_IN]-(destinationAirportDay)
MERGE (destination)-[:HAS_DAY]->(destinationAirportDay)

REMOVE flight:Process
RETURN count(*)

',{limit:500}
)
----

[.notes]
--
Here we include the refactoring code for creating the _AirportDay_ nodes/relationships in our call to _apoc.periodic.commit()_ where we specify that the refactoring code will create 500 _AirportDay_ nodes in a single transaction.
--

=== Result of the batch processing

image::APOCBatchExecution.png[APOCBatchExecution,width=800,align=center]

[.student-exercise]
== Exercise 7: Refactoring large graphs

[.small]
--
In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-neo4j-modeling-exercises]

and follow the instructions for Exercise 7.

[NOTE]
This exercise has 8 steps.
Estimated time to complete: 30 minutes.
--

[.quiz]
== Check your understanding

=== Question 1

[.statement]
Suppose you want to create _Person_ nodes in the graph, each with a unique value for the _personID_ property . What must you do to ensure that nodes are unique?

[.statement]
Select the correct answers.

[%interactive.answers]
- [ ] Test the existence of the _Person_ node with the _personID_ property value before you use `CREATE` to create it.
- [ ] Create an existence constraint for the _personID_ property of the _Person_ node.
- [x] Use `MERGE` to create the _Person_ node with a unique property value specified for _personID_.
- [x] Create a uniqueness constraint for the _personID_ property of the _Person_ node.

=== Question 2

[.statement]
In many cases, how will refactoring change the graph?

[.statement]
Select the correct answers.

[%interactive.answers]
- [x] Add more nodes to the graph to represent the same data.
- [ ] Reduce nodes by consolidating data.
- [x] Add more relationships to the graph.
- [ ] Reduce the number of relationships in the graph.

=== Question 3

[.statement]
What is the recommended method for batching refactorings for a large graph?
[.statement]
Select the correct answer.

[%interactive.answers]
- [ ] Use the `FOREACH` clause in Cypher.
- [ ] Use a `DO WHILE` clause in Cypher.
- [ ] Use _db.iterate()_.
- [x] Use _apoc.periodic.commit()_.

[.summary]
== Summary

You should now be able to:
[square]
* Create constraints that help with performance of node creation and queries.
* Determine if a query can be improved.
* Write Cypher code to refactor the data model in the graph.
* Create indexes that will help with query performance.
* Refactor a graph by creating intermediate nodes.
* Refactor a graph by specializing relationships.
* Perform batch processing to refactor a large graph.
