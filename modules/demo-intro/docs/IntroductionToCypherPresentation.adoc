= Introduction to Cypher
:doctype: book
:toc: left
:toclevels: 3
:prevsecttitle: Development Environment
:prevsect: 3
:currsect: 4
:nextsecttitle: Advanced Queries
:nextsect: 5
:experimental:
:imagesdir: ../images
:manual: http://neo4j.com/docs/developer-manual/current
:manual-cypher: {manual}/cypher
// wordpress
:private:
:slug: demo-introduction-cypher

== Overview

At the end of this module, you should be able to write Cypher statements to:
[square]
* Retrieve nodes from the graph.
* Filter nodes retrieved using labels and property values of nodes.
* Retrieve property values from nodes in the graph.
* Filter nodes retrieved using relationships.

[.notes]
--
Show the students:

* Neo4j Developer Manual: https://neo4j.com/docs/cypher-manual/current/
* Cypher RefCard: http://neo4j.com/docs/cypher-refcard/current/
--

=== References

Throughout this training, you should refer to:
[square]
* https://neo4j.com/docs/cypher-manual/current/[Neo4j Cypher Manual^]
* http://neo4j.com/docs/cypher-refcard/current/[Cypher Reference card^]

== What is Cypher?

[.two-columns]
--
* Declarative query language
* Focuses on _what_, not how to retrieve
* Uses keywords such as `MATCH`, `WHERE`, `CREATE`
* Runs in the database server for the graph
* ASCII art to represent nodes and relationships

image::overview.svg[Overview,align=center]
--

=== Cypher is ASCII Art

image::asciiart.svg[Cypher AsciiArt,width=450px,align=center]

[source.big,cypher]
----
(A)-[:LIKES]->(B),(A)-[:LIKES]->(C),(B)-[:LIKES]->(C)
(A)-[:LIKES]->(B)-[:LIKES]->(C)<-[:LIKES]-(A)
----

[.notes]
--
Being a declarative language, Cypher focuses on the clarity of expressing *what* to retrieve from a graph, not on *how* to retrieve it.
You can think of Cypher as mapping English language sentence structure to patterns in a graph.
--

=== Cypher is readable

image::cypher-readable.svg[Cypher Readable,100%,align=center]

[.notes]
--
For example, the nouns are nodes of the graph, the verbs are the relationships in the graph, and the adjectives and adverbs are the properties.
--

=== Nodes

[.two-columns]
--
[source,cypher-syntax]
----
()
(p)
(l)
(n)
----

image::cypher-nodes.svg[Nodes,width=700,align=center]
--

=== Labels

[.is-one-third.left]
--
[source,cypher]
----
(:Person)
(p:Person)
(:Location)
(l:Location)
(n:Residence)
(x:Location:Residence)
----

image::NodeLabels.png[Labels,350px,align=center]
--

[.is-two-thirds.right]
image::Labels.svg[Nodes,75%,align=center]

=== Comments in Cypher

[source,cypher]
----
()                  // anonymous node not be referenced later in the query
(p)                 // variable p, a reference to a node used later
(:Person)           // anonymous node of type Person
(p:Person)          // p, a reference to a node of type Person
(p:Actor:Director)  // p, a reference to a node of types Actor and Director
----

== Examining the data model

image::call_db.schema.png[Schema,width=700,align=center,role=border]

=== Using MATCH to retrieve nodes

[source,cypher]
----
MATCH (n) // returns all nodes in the graph
RETURN n
----

[source,cypher]
----
MATCH (p:Person) // returns all Person nodes in the graph
RETURN p
----

image::person-nodes-graph.png[Person Nodes Graph,height=375,align=center,role=border]

=== Viewing nodes as table data

image::PersonNodes-table.png[width=700,align=center,role=border]

[.title.gold]
== Exercise 1: Retrieving Nodes

In Neo4j Browser:

kbd:[:play intro-neo4j-exercises]

Then follow instructions for Exercise 1.

=== Properties

image::Properties.png[width=700,align=center]

=== Examining property keys

[source,cypher]
----
CALL db.propertyKeys
----

image::call_db.PropertyKeys.png[width=700,align=center,role=border]

=== Retrieving nodes filtered by a property value - 1

Find all _people_ born in 1970, _returning the nodes_:

[source.fit,cypher]
----
MATCH (p:Person {born: 1970})
RETURN p
----

image::MatchPersonBorn1970.png[height=250,align=center,role=border]

=== Retrieving nodes filtered by a property value - 2

Find all _movies_ released in _2003_ with the tagline, +
_Free your mind_, returning the nodes:

[source,cypher]
----
MATCH (m:Movie {released: 2003, tagline: 'Free your mind'})
RETURN m
----

image::MatchTaglineFreeYourMind.png[height=225,align=center,role=border]

=== Returning property values

Find all people born in _1965_ and return their names:

[source,cypher]
----
MATCH (p:Person {born: 1965})
RETURN p.name, p.born
----

image::MatchPersonBorn1965.png[width=700,align=center,role=border]

=== Specifying aliases

[source,cypher]
----
MATCH (p:Person {born: 1965})
RETURN p.name AS name, p.born AS `birth year`
----

image::AliasBirthDate.png[width=880,align=center,role=border]

[.title.gold]
== Exercise 2: Filtering queries using property values

In Neo4j Browser:

kbd:[:play intro-neo4j-exercises]

Then follow instructions for Exercise 2.

=== Relationships

image::Relationships.svg[width=700,align=center]

=== ASCII art for nodes and relationships

[source,cypher]
----
()          // a node
()--()	    // 2 nodes have some type of relationship
()-->()	    // the first node has a relationship to the second node
()<--()	    // the second node has a relationship to the first node
----

=== Querying using relationships

image::Relationships.svg[height=350,align=center]

[source,cypher]
----
MATCH (p:Person)-[:LIVES_AT]->(h:Residence)
RETURN p.name, h.address
----

[source,cypher]
----
MATCH (p:Person)--(h:Residence) // any relationship
RETURN p.name, h.address
----

=== Examining relationships

[source,cypher]
----
CALL db.schema
----

image::call_db.schema.png[width=645,align=center,role=border]

=== Using a relationship in a query

Find all people who acted in the movie, _The Matrix_, returning the nodes and relationships found:

[source,cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie {title: 'The Matrix'})
RETURN p, rel, m
----

image::MatchRelationshipActedIn.png[height=300,align=center,role=border]

=== Querying by multiple relationships

Find all movies that _Tom Hanks_ acted in or directed and return the title of the move:

[source,cypher]
----
MATCH (p:Person {name: 'Tom Hanks'})-[:ACTED_IN |:DIRECTED]->(m:Movie)
RETURN p.name, m.title
----

image::TomHanksActedDirected.png[height=300,align=center,role=border]

=== Using anonymous nodes in a query

Find all people who acted in the movie, _The Matrix_ and return their names:

[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(:Movie {title: 'The Matrix'})
RETURN p.name
----

image::AnonymousMovieNode.png[height=300,align=center,role=border]

=== Using an anonymous relationship for a query

Find all people who have any type of relationship to the movie, _The Matrix_ and return the nodes:

[source,cypher]
----
MATCH (p:Person)-->(m:Movie {title: 'The Matrix'})
RETURN p, m
----

image::AllRelationshipsMatrix.png[height=200,align=center,role=border]

[.tooltip]
Connect result nodes enabled in Neo4j Browser

=== Retrieving relationship types

Find all people who have any type of relationship to the movie, _The Matrix_ and return the name of the person and their relationship type:

[source,cypher]
----
MATCH (p:Person)-[rel]->(:Movie {title:'The Matrix'})
RETURN p.name, type(rel)
----

image::MatrixRelationshipTypes.png[height=200,align=center,role=border]

=== Retrieving properties for a relationship - 1

image::REVIEWEDProperties.svg[width=880,align=center]

=== Retrieving properties for a relationship - 2

Find all people who gave the movie, _The Da Vinci Code_, a rating of _65_, returning their names:

[source,cypher]
----
MATCH (p:Person)-[:REVIEWED {rating: 65}]->(:Movie {title: 'The Da Vinci Code'})
RETURN p.name
----

image::DaVinciCode.png[height=220,align=center,role=border]

=== Using patterns for queries - 1

[.is-one-third.left]
image::FollowsRelationships.png[width=350px,align=center]

[.is-two-thirds.right]
--
Find all people who follow _Angela Scope_, returning the nodes:

[source,cypher]
----
MATCH (p:Person)-[:FOLLOWS]->(:Person {name:'Angela Scope'})
RETURN p
----

image::AngelaFollowers.png[width=600,align=center,role=border]
--

=== Using patterns for queries - 2

[.is-one-third.left]
image::FollowsRelationships.png[width=350px,align=center]

[.is-two-thirds.right]
--
Find all people who _Angela Scope_ follows, returning the nodes:

[source,cypher]
----
MATCH (p:Person)<-[:FOLLOWS]-(:Person {name:'Angela Scope'})
RETURN p
----

image::FollowedByAngela.png[width=600,align=center,role=border]
--

=== Querying by any direction of the relationship

[.is-one-third.left]
image::FollowsRelationships.png[width=350px,align=center]

[.is-two-thirds.right]
--
Find all people who _Angela Scope_ follows, returning the nodes:

[source,cypher]
----
MATCH (p1:Person)-[:FOLLOWS]-(p2:Person {name:'Angela Scope'})
RETURN p1, p2
----

image::AngelaFollowsAndFollowers.png[width=550,align=center,role=border]
--

=== Traversing relationships - 1

[.is-one-third.left]
image::FollowsRelationships.png[width=350px,align=center]

[.is-two-thirds.right]
--
Find all people who follow anybody who follows _Jessica Thompson_ returning the people as nodes:

[source,cypher]
----
MATCH  (p:Person)-[:FOLLOWS]->(:Person)-[:FOLLOWS]->
       (:Person {name:'Jessica Thompson'})
RETURN p
----

image::Traversing.png[width=550,align=center,role=border]
--

=== Traversing relationships - 2

[.is-one-third.left]
image::FollowsRelationships.png[width=350px,align=center]

[.is-two-thirds.right]
--
Find  the path that includes all people who follow anybody who follows _Jessica Thompson_ returning the path:

[source,cypher]
----
MATCH   path = (:Person)-[:FOLLOWS]->(:Person)-[:FOLLOWS]->
               (:Person {name:'Jessica Thompson'})
RETURN  path
----

image::ReturnPath.png[width=275,align=center,role=border]
--

[.tooltip]
Sub-graph

=== Using relationship direction to optimize a query

Find all people that acted in a movie and the directors for that same movie, returning the name of the actor, the movie title, and the name of the director:

[source,cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person)
RETURN a.name, m.title, d.name
----

image::TraversalInTwoDirections.png[width=350,align=center,role=border]

=== Cypher style recommendations - 1

Here are the *Neo4j recommended* Cypher coding standards that we use in this training:

* Node labels are CamelCase and case-sensitive (examples: `Person`, `NetworkAddress`).
* Property keys, variables, parameters, aliases, and functions are camelCase case-sensitive (examples: `businessAddress`, `title`).
* Relationship types are in upper-case and can use the underscore. (examples: `ACTED_IN`, `FOLLOWS`).
* Cypher keywords are upper-case (examples: `MATCH`, `RETURN`).

=== Cypher style recommendations - 2

Here are the *Neo4j recommended* Cypher coding standards that we use in this training:

* String constants are in single quotes (with exceptions).
* Specify variables only when needed for use later in the Cypher statement.
* Place named nodes and relationships (that use variables) before anonymous nodes and relationships in your `MATCH` clauses when possible.
* Specify anonymous relationships with `+-->+`, `--`, or `+<--+`

[source,cypher]
----
MATCH (:Person {name: 'Diane Keaton'})-[movRel:ACTED_IN]->
(:Movie {title:"Something's Gotta Give"})
RETURN movRel.roles
----

[.title.gold]
== Exercise 3: Filtering queries using relationships

In Neo4j Browser:

kbd:[:play intro-neo4j-exercises]

Then follow instructions for Exercise 3.

[.title.purple]
== Check your understanding

=== Question 1

[.statement]
Suppose you have a graph that contains nodes representing customers and other business entities for your application.
The node label in the database for a customer is _Customer_. Each _Customer_ node has a property named _email_ that contains the customer's email address.
What Cypher query do you execute to return the email addresses for all customers in the graph?

Select the correct answer.

[%interactive.answers]
- [ ] [src-cypher]`MATCH (n) RETURN n.Customer.email`
- [x] [src-cypher]`MATCH (c:Customer) RETURN c.email`
- [ ] [src-cypher]`MATCH (Customer) RETURN email`
- [ ] [src-cypher]`MATCH (c) RETURN Customer.email`

=== Question 2

[%interactive.statement]
Suppose you have a graph that contains _Customer_ and _Product_ nodes. A _Customer_ node can have a _BOUGHT_ relationship with a _Product_ node.
_Customer_ nodes can have other relationships with _Product_ nodes.
A _Customer_ node has a property named _customerName_.
A _Product_ node has a property named _productName_.
What Cypher query do you execute to return all of the products (by name) bought by customer 'ABCCO'.

Select the correct answer.

[%interactive.answers]
- [ ] [src-cypher]`MATCH (c:Customer {customerName: 'ABCCO'}) RETURN c.BOUGHT.productName`
- [ ] [src-cypher]`MATCH (:Customer 'ABCCO')-[:BOUGHT]->(p:Product) RETURN p.productName`
- [ ] [src-cypher]`MATCH (p:Product)<-[:BOUGHT_BY]-(:Customer 'ABCCO') RETURN p.productName`
- [x] [src-cypher]`MATCH (:Customer {customerName: 'ABCCO'})-[:BOUGHT]->(p:Product) RETURN p.productName`

=== Question 3

When [.underline]#must# you use a variable in a `MATCH` clause?

Select the correct answer.

[%interactive.answers]
- [ ] When you want to query the graph using a node label.
- [ ] When you specify a property value to match the query.
- [x] When you want to use the node or relationship to return a result.
- [ ] When the query involves 2 types of nodes.

== Summary

You should now be able to write Cypher statements to:
[square]
* Retrieve nodes from the graph.
* Filter nodes retrieved using property values of nodes.
* Retrieve property values from nodes in the graph.
* Filter nodes retrieved using relationships.
