= Deleting Nodes and Relationships
:slug: 11-deleting-nodes-and-relationships
:doctype: book
:toc: left
:toclevels: 4
:imagesdir: ../images
:page-slug: {slug}
:page-layout: training
:page-quiz:
:page-module-duration-minutes: 20

== About this module

[.notes]
--
You have just learned how to create nodes, relationships, and properties for them.
Next, you will learn how to delete nodes and relationships in the graph.
--

At the end of this module, you should be able to write Cypher statements to:
[square]

* Delete a node.
* Delete a relationship.
* Delete a node and its relationships.

[IMPORTANT]
Because the code examples in this lesson modify the database, it is recommended that you [.underline]#do not# execute them against your database as you will be doing so in the hands-on exercises.

== Deleting nodes

[.notes]
--
Suppose you have created a _Person_ node for Jane Doe:
--

[source,Cypher,role=nocopy noplay]
----
CREATE (p:Person {name: 'Jane Doe'})
----

[.statement]
You delete this node as follows:

[source,Cypher,role=nocopy noplay]
----
MATCH (p:Person)
WHERE p.name = 'Jane Doe'
DELETE p
----

ifndef::env-slides[]
Here is the result:
endif::[]

image::DeleteJaneDoe.png[DeleteJaneDoe,width=800,align=center]

[.notes]
--
You can delete a node provided:

. You can obtain a reference to it (typically using `MATCH`).
. The node has no relationships.

So if you accidentally created multiple nodes in the graph, you can retrieve them with a `MATCH` clause and delete them.
--

== Deleting relationships

[.notes]
--
In our graph, the _Katie Holmes_ node already has an _:ACTED_IN_ relationship with the _Batman Begins_ movie.
Perhaps we were over zealous with the creation of relationships between nodes.
For example, what if we had this code where relationships were created:
--

[source,Cypher,role=nocopy noplay]
----
MATCH (a:Person), (m:Movie)
WHERE a.name = 'Katie Holmes' AND m.title = 'Batman Begins'
CREATE (a)-[:WROTE]->(m)
CREATE (a)-[:DIRECTED]->(m)
WITH a
MATCH (a)-[rel]-()
RETURN type(rel)
----

ifndef::env-slides[]
Here is the result of executing this code:
endif::[]

image::TooManyRels.png[TooManyRels,width=300,align=center]

[.statement]
We see that there are now three relationships from _Katie Holmes_ and _Batman Begins_.

=== Example: Finding and deleting relationships

[.notes]
--
Provided you have a reference to a relationship, you can delete it.
So for example, we can find the relationships that we want to delete and then delete them as follows:
--

[source,Cypher,role=nocopy noplay]
----
MATCH (a:Person)-[rel:WROTE | DIRECTED]->(m:Movie)
WHERE a.name = 'Katie Holmes' AND m.title = 'Batman Begins'
DELETE rel
RETURN a, m
----

ifndef::env-slides[]
Here is the result of executing this code:
endif::[]

image::DeleteRels.png[DeleteRels,width=500,align=center]

=== Example: Confirming deletion

[.notes]
--
You can confirm that there is only one relationship associated with the _Katie Holmes_ node as follows:
--

[source,Cypher,role=nocopy noplay]
----
MATCH (a:Person)-[rel]-()
WHERE a.name = 'Katie Holmes'
RETURN count(rel) AS `Number of Katie Holmes relationships:`
----

ifndef::env-slides[]
Here is the result of executing this code:
endif::[]

image::KatieRels.png[KatieRels,width=800,align=center]

== Deleting nodes and relationships

[.notes]
--
The most efficient way to delete a node and its corresponding relationships is to specify `DETACH DELETE`.  
When you specify `DETACH DELETE` for a node, the relationships to and from the node are deleted, then the node is deleted.

If we were to attempt to delete the _Liam Neeson_ node without first deleting its relationships:
--

[source,Cypher,role=nocopy noplay]
----
MATCH (p:Person)
WHERE p.name = 'Liam Neeson'
DELETE p
----

[.statement]
We would see this error:

image::LiamNeesonDeleteError.png[LiamNeesonDeleteError,width=800,align=center]

== Using `DETACH DELETE`

[.notes]
--
Here we delete the _Liam Neeson_ node [underline]#and# its relationships to any other nodes:
--

[source,Cypher,role=nocopy noplay]
----
MATCH (p:Person)
WHERE p.name = 'Liam Neeson'
DETACH DELETE  p
----

ifndef::env-slides[]
Here is the result of running this Cypher statement:
endif::[]

[.is-half.left-column]
--
image::DeleteLiamNeeson.png[DeleteLiamNeeson,width=600,align=center]
--

[.notes]
--
And here is what the _Batman Begins_ node and its relationships now look like. There is only one actor, _Michael Caine_ connected to the movie.
--

[.is-half.right-column]
--
image::BatmanBeginsRelationships.png[BatmanBeginsRelationships,width=700,align=center]
--

[.student-exercise]
== Exercise 11: Deleting nodes and relationships

In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-intro-neo4j-exercises]

and follow the instructions for Exercise 11.

[NOTE]
This exercise has 6 steps.
Estimated time to complete: 10 minutes.

[.quiz]
== Check your understanding

=== Question 1

[.statement]
You can delete a node with the `DELETE` clause.
What must you do before executing the `DELETE` clause?

[.statement]
Select the correct answers.

[%interactive.answers]
- [x] Get a reference to the node.
- [x] Ensure the node has no relationships.
- [ ] Remove all labels from the node.
- [ ] Remove all properties from the node

=== Question 2

[.statement]
What happens if you attempt to delete a node using `DELETE`, and the node has existing relationships?

[.statement]
Select the correct answers.

[%interactive.answers]
- [x] You receive a client error.
- [ ] The node is deleted.
- [ ] The node's relationships are deleted.
- [x] Nothing is changed in the graph.

=== Question 3

[.statement]
Suppose you retrieve a node, _n_ in the graph that is related to other nodes. What Cypher clause do you write to delete this node and its relationships in the graph?

[.statement]
Select the correct answer.

[%interactive.answers]
- [ ] `DELETE n`
- [ ] `DELETE n WITH RELATIONSHIPS`
- [ ] `REMOVE n`
- [x] `DETACH DELETE n`

[.summary]
== Summary

You should now be able to write Cypher statements to:
[square]

* Delete a node.
* Delete a relationship.
* Delete a node and its relationships.
