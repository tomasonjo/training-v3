= Using Indexes
:slug: 14-using-indexes
:doctype: book
:toc: left
:toclevels: 4
:imagesdir: ../images
:module-next-title: Using Query Best Practices

== About this module

[.notes]
--
You have learned how to define both uniqueness and existence constraints for your data.
A uniqueness constraint is also an index where the default implementation is a b-tree structure.
Next, you will learn about creating property and full-text schema indexes in the graph.
--

[.small]
--
At the end of this module, you should be able to:
[square]
* Describe when indexes are used in Cypher.
* Create a single property index.
* Create a multi-property index.
* Create a full-text schema index.
* Use a full-text schema index.
* Manage indexes:
** List indexes.
** Drop an index.
** Drop a full-text schema index.
--

== Indexes in Neo4j

[.notes]
--
The uniqueness and node key constraints that you add to a graph are essentially single-property and composite indexes respectively.
Indexes are used to improve initial node lookup performance, but they require additional storage in the graph to maintain and also add to the cost of creating or modifying property values that are indexed.
Indexes store redundant data that points to nodes with the specific property value or values.
Unlike SQL, there is no such thing as a primary key in Neo4j.
You can have multiple properties on nodes that must be unique.
--

Single-property indexes are used for:

* Equality checks `=`
* Range comparisons `>`,`+>=+`,`<`, `+<=+`
* List membership `IN`
* String comparisons `STARTS WITH`, `ENDS WITH`, `CONTAINS`
* Existence checks `exists()`
* Spatial distance searches `distance()`
* Spatial bounding searches `point()`

Composite indexes are used only for equality checks and list membership.

[.notes]
--
In this course, we introduce the basics of Neo4j b-tree indexes, but you should consult the Neo4j _Operations Manual_ for more details about creating and maintaining indexes.

[NOTE]
Because index maintenance incurs additional overhead when nodes are created, Neo4j recommends that for large graphs, indexes are created after the data has been loaded into the graph.
--

=== Indexes for range searches

[.notes]
--
When you add an index for a property of a node, it can greatly reduce the number of nodes the graph engine needs to visit in order to satisfy a query.

In this query we are testing the value of the _released_ property of a _Movie_ node using ranges:
--

[source,cypher]
----
MATCH (m:Movie)
WHERE 1990 < m.released < 2000
SET m.videoFormat = 'DVD'
----

[.notes]
--
If there is an index on the _released_ property, the graph engine will find the pointers to all nodes that satisfy the query without having to visit all of the nodes:
--

image::IndexForRanges.png[IndexForRanges,width=400,align=center]

== Creating a single-property index

[.notes]
--
You create an index to improve graph engine performance.
A uniqueness constraint on a property is an index so you need not create an index for any properties you have created uniqueness constraints for.
An index on its own does [.underline]#not# guarantee uniqueness.

Here is an example of how we create a single-property index on the _released_ property of all nodes of type _Movie_:
--

[source,cypher]
----
CREATE INDEX MovieReleased FOR (m:Movie) ON (m.released)
----

[.notes]
--
Notice that just as for constraints, a best practice is to specify a name for the index.
In this case, the name is _MovieReleased_.
--

ifndef::backend-revealjs[]
With the result:
endif::[]

image::CreateSingle-propertyIndex.png[CreateSingle-propertyIndex,width=1000,align=center]

== Creating composite indexes

[.notes]
--
If a set of properties for a node must be unique for every node, then you should create a constraint as a node key, rather than an index.

If, however, there can be duplication for a set of property values, but you want faster access to them, then you can create a composite index.
A composite index is based upon multiple properties for a node.

Suppose we added the property, _videoFormat_ to every _Movie_ node and set its value, based upon the released date of the movie as follows:
--

[source,cypher]
----
MATCH (m:Movie)
WHERE m.released >= 2000
SET m.videoFormat = 'DVD';
MATCH (m:Movie)
WHERE m.released < 2000
SET m.videoFormat = 'VHS'
----

ifndef::backend-revealjs[]
With the result:
endif::[]

image::TwoStatements.png[TwoStatements,width=1200,align=center]

[.notes]
--

[NOTE]
Notice that in the above Cypher statements we use the semi-colon `;` to separate Cypher statements.
In general, you need not end a Cypher statement with a semi-colon, but if you want to execute multiple Cypher statements, you must separate them. You have already used the semi-colon to separate Cypher statements when you loaded the _Movie_ database in the training exercises.
--

=== Example: Creating a composite index

[.notes]
--
Now that the graph has _Movie_ nodes with both the properties, _released_ and _videoFormat_, we can create a composite index on these properties as follows:
--

[source,cypher]
----
CREATE INDEX MovieReleasedVideoFormat FOR (m:Movie) ON (m.released, m.videoFormat)
----

ifndef::backend-revealjs[]
With the result:
endif::[]

image::CreateCompositeIndex.png[CreateCompositeIndex,width=1000,align=center]

== Creating full-text schema indexes

[.notes]
--
A full-text schema index is based upon string values only, but they provide additional search capabilities that you do not get from property indexes.
A full-text schema index can be used for:
--

[square]
* Node or relationship properties.
* Single property or multiple properties.
* Single or multiple types of nodes (labels).
* Single or multiple types of relationships.

[.notes]
--
Rather than using Cypher syntax to create a full-text schema index, you call a procedure to create the index.
The index is not used implicitly when you query the graph.
You must call a procedure to start a query that uses the index.
By default, the underlying implementation of a full-text schema index is Lucene.
You can change the underlying index provider of any index.
--

=== Example: Creating a full-text schema index

[.notes]
--
Here is an example where we create a full-text schema index on data in _title_ property of _Movie_ nodes and data in the _name_ property of _Person_ nodes:
--

[source,cypher]
----
CALL db.index.fulltext.createNodeIndex(
      'MovieTitlePersonName',['Movie', 'Person'], ['title', 'name'])
----

ifndef::backend-revealjs[]
The result returned shows nothing exceptional:
endif::[]

image::CreateFullTextIndex1.png[CreateFullTextIndex1,width=900,align=center]

== Retrieving configured indexes

[.notes]
--
After creating a full-text schema index, you can always get of listing of all existing indexes:
--

[%unbreakable]
--
[source,cypher]
----
CALL db.index.fulltext.createNodeIndex(
     'MovieTitlePersonName',['Movie', 'Person'], ['title', 'name'])
----

[.statement]
And here we see our newly-created full-text schema index:

image::CreateFullTextIndex2.png[CreateFullTextIndex2,width=1300,align=center]
--

[.notes]
--
Just as you can create a full-text schema index on properties of nodes, you can create a full-text schema index on properties of relationships.
To do this you use `CALL db.indexfulltext.createRelationshipIndex()`.
--

== Using a full-text schema index

[.notes]
--
To use a full-text schema index, you must call the query procedure that uses the index.

Here is an example where we want to find all movies and person names that contain the string _Jerry_:
--

[source,cypher]
----
CALL db.index.fulltext.queryNodes(
     'MovieTitlePersonName', 'Jerry') YIELD node
RETURN node
----

[.notes]
--
Notice that we specify `YIELD` after calling the procedure. This enables us to use return values from the procedure.
In this case, we return all nodes that are found in the graph that have either a _title_ property or _name_ property containing the string, _Jerry_.
--

ifndef::backend-revealjs[]
And here is the result:
endif::[]

image::UseFullTextIndex1.png[UseFullTextIndex1,width=600,align=center]

== Retrieving the score for a full-text search

[.notes]
--
When a full-text schema index is used, it calculates a "hit score" that represents the closeness of the values in the graph to the query string.

Here is an example:
--

[%unbreakable]
--
[source,cypher]
----
CALL db.index.fulltext.queryNodes(
     'MovieTitlePersonName', 'Matrix') YIELD node, score
RETURN node.title, score
----

[.statement]
The nodes returned have a Lucene score based upon how much of _Matrix_ was part of the _title_:

image::UseFullTextIndex2.png[UseFullTextIndex2,width=900,align=center]
--

=== Example: Searching on a particular property

[.notes]
--
With full-text indexes created, you can also specify which property you want to search for. Here is an example where we are looking for _Jerry_, but only as a _name_ property of a _Person_ node:
--

[source,cypher]
----
CALL db.index.fulltext.queryNodes(
     'MovieTitlePersonName', 'name: Jerry') YIELD node
RETURN node
----

ifndef::backend-revealjs[]
Here is what is returned:
endif::[]

image::UseFullTextIndex3.png[UseFullTextIndex3,width=900,align=center]

[.notes]
--
Please see the _Cypher Reference Manual_ for more on using full-text schema indexes.
--

== Managing Indexes

[.statement]
You have already seen the three types of indexes in our database thus far using this Cypher statement:

[source,cypher]
----
CALL db.indexes()
----

ifndef::backend-revealjs[]
Here is what is returned:
endif::[]

image::ManagingIndexes1.png[ManagingIndexes1,width=1300,align=center]

== Dropping an index

[.notes]
--
To drop an index on a property, you simply use the `DROP INDEX` clause, specifying the name of the index:
--

[source,cypher]
----
DROP INDEX MovieReleasedVideoFormat
----

ifndef::backend-revealjs[]
With the result:
endif::[]

image::ManagingIndexes2.png[ManagingIndexes2,width=900,align=center]

== Dropping a full-text schema index

[.notes]
--
To drop a full-text schema index, you must call the procedure. Here we drop the index that we created earlier:
--

[%unbreakable]
--
[source,cypher]
----
CALL db.index.fulltext.drop('MovieTitlePersonName')
----

ifndef::backend-revealjs[]
With the result:
endif::[]

image::ManagingIndexes3.png[ManagingIndexes3,width=900,align=center]
--

=== Confirming dropped indexes

[.statement]
You must list the indexes to confirm that it was dropped:

[source,cypher]
----
CALL db.indexes()
----

ifndef::backend-revealjs[]
Here is what is returned:
endif::[]

image::ManagingIndexes4.png[ManagingIndexes4,width=1300,align=center]

[.student-exercise]
== Exercise 14: Creating indexes

In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-intro-neo4j-exercises]

and follow the instructions for Exercise 14.

[NOTE]
This exercise has 7 steps.
Estimated time to complete: 30 minutes.

[.quiz]
== Check your understanding

=== Question 1

[.statement]
What Cypher code below will create a unique index on the _name_ property of the Person node?

[.statement]
Select the correct answer.

[%interactive.answers]
- [ ] [src-cypher]`CREATE INDEX PersonNameIndex FOR (p:Person) ON (p.name)`
- [ ] [src-cypher]`CREATE INDEX PersonNameIndex FOR (p:Person) ON (p.name) ASSERT p.name IS UNIQUE`
- [x] [src-cypher]`CREATE CONSTRAINT PersonNameConstraint ON (p:Person) ASSERT p.name IS UNIQUE`
- [ ] [src-cypher]`CALL db.index.full-text.createNodeIndex('PersonName',['Person'], ['name'])`

=== Question 2

[.statement]
What makes creating a full-text schema index different from creating a property index?

[.statement]
Select the correct answers.

[%interactive.answers]
- [x] Full-text schema indexes can use relationship properties.
- [ ] Full-text schema indexes can check for uniqueness.
- [x] Full-text schema indexes can use multiple types of nodes for the index.
- [ ] Full-text schema indexes can be used to ensure the existence of a property.

=== Question 3

[.statement]
What is the difference between a node key and a composite index?

[.statement]
Select the correct answer.

[%interactive.answers]
- [ ] A composite index can utilize more than one type of node.
- [ ] A composite index can use relationship properties.
- [x] A composite index does not enforce uniqueness.
- [ ] A composite index can enforce existence.

[.summary]
== Summary

[.small]
--
You should now be able to:

[square]
* Describe when indexes are used in Cypher
* Create a single property index
* Create a multi-property index
* Create a full-text schema index
* Use a full-text schema index
* Manage indexes
** List indexes
** Drop an index
** Drop a full-text schema index
--
