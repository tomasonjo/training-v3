= Merging Data
:slug: 12-merging-data
:doctype: book
:toc: left
:toclevels: 4
:experimental:
:imagesdir: ../images
:module-next-title: Defining Constraints for your Data

== About this module

[.notes]
--
You have learned how to create nodes and relationships in the graph.
When you create a node you want to ensure that there is only one instance of that node in the graph.
You also want to ensure that you do not create unnecessary or duplicate relationships in the graph.
Next, you will learn how to merge data in the graph.
--

At the end of this module, you should be able to write Cypher statements to:
[square]
* Merge data in a graph by:
** Creating nodes.
** Creating relationships.

== Creating data in the graph
[.notes]
--
Thus far, you have learned how to create nodes, labels, properties, and relationships in the graph.
You can use `MERGE` to either create new nodes and relationships or to make changes to existing nodes and relationships.

For example, how the graph engine behaves when a duplicate element is created depends on the type of element.
You have not yet learned about defining constraints in the graph.
Constraints can be used to eliminate duplication of nodes and you will learn about them later in this course.

Here are some facts about creating nodes and relationships in the graph when no constraints have been defined.
--

ifndef::env-slides[]
{set:cellbgcolor:white}
[frame="none",grid=none,width="90%"cols="40,60",stripes=none]
|===
h|If you use `CREATE`:
h|The result is:
|Node
|If a node with the same property values exists, a duplicate node is created.
|Label
|If the label already exists for the node, the node is not updated.
|Property
|If the node or relationship property already exists, it is updated with the new value.
*Note:* If you specify a set of properties to be created using `=` rather than `+=`, existing properties are removed if they are not included in the set.
|Relationship
|If the relationship exists, a duplicate relationship is created.
|===
{set:cellbgcolor!}
endif::[]

ifdef::env-slides[]
Node creation behaviors:

[.statement]
*Node*: If a node with the same property values exists, a duplicate node is created.
[.statement]
*Label*: If the label already exists for the node, the node is not updated.
[.statement]
*Property*: If the node or relationship property already exists, it is updated with the new value. Note that if you specify a set of properties to be created using `=` rather than `+=`, it could remove existing properties if they are not included in the set.
[.statement]
*Relationship*: If the relationship exists, a duplicate relationship is created.
endif::[]


== Using `MERGE`

[.notes]
--
The `MERGE` clause is used to find nodes or patterns in the graph.
If the node or pattern is not found, by default, it is created.
--
You use the simple `MERGE` clause to:

[square]
* Create a unique node based on label and key information for a property or set of properties.
* Update a node based on label and key information for a property or set of properties.
* Create a unique relationship between two nodes.
* Create a unique node and relationship in the context of another node.

== Syntax: Using `MERGE` to create nodes

[.notes]
--
Here is the simplified syntax for the `MERGE` clause for creating a node:
--

[source.big,cypher]
----
MERGE (variable:Label{nodeProperties})
RETURN variable
----

[.notes]
--
If there is an existing node with  _Label_ and _nodeProperties_  found in the graph, no node is created.
If, however the node is not found in the graph, then the node is created.

When you specify _nodeProperties_ for `MERGE`, you should only use properties that satisfy some sort of uniqueness constraint.
You will learn about uniqueness constraints later in this course.

Here is what we currently have in the graph for the _Person_, _Michael Caine_. 
This node has values for _name_ and _born_. Notice also that the label for the node is _Person_.
--

[source,cypher]
----
MATCH (p:Person)
WHERE p.name = 'Michael Caine'
RETURN p
----

image::MichaelCaine.png[MichaelCaine,width=500,align=center]

=== Example: Using `MERGE`

[.notes]
--
In this example, we use `MERGE` to find a node with the _Actor_ label with the key property _name_ of _Michael Caine_, and we set the _born_ property to _1933_. Our data model has never used the label, _Actor_ so this is a new entity type in our graph.
--

[source,cypher]
----
MERGE (a:Actor {name: 'Michael Caine'})
SET a.born = 1933
RETURN a
----

[.notes]
--
Here is the result of running this Cypher example.
We do not find a node with the label _Actor_ so the graph engine creates one.
--

image::MergeActorMichaelCaine.png[MergeActorMichaelCaine,width=500,align=center]


[.small]
--
[NOTE]
A best practice when using `MERGE` is to only specify properties that have unique values and unique labels.
--

=== Repeating the same `MERGE`

[.notes]
--
If we were to repeat this `MERGE` clause, no additional _Actor_ nodes would be created in the graph.

At this point, however, we have two _Michael Caine_ nodes in the graph, one of type _Person_, and one of type _Actor_:
--

[.is-half.left-column]
--
image::TwoMichaelCaines.png[TwoMichaelCaines,width=800,align=center]
--

[.notes]
--
Be mindful that node labels and the properties for a node are significant when merging nodes.

If we were to run `MERGE` code again:
--

[.is-half.right-column]
--
[source,cypher]
----
MERGE (a:Actor {name: 'Michael Caine'})
SET a.born = 1933
WITH a
MATCH (p)
WHERE p.name = 'Michael Caine'
RETURN p
----

image::OnlyTwoMichaelCaines.png[OnlyTwoMichaelCaines,width=800,align=center]
--

[.notes]
--
We would find that the _Michael Caine_ node with the label _Actor_ is not created.
The `MERGE` found this node in the graph and did not create a new one.
--

== Syntax: Using `MERGE` to create relationships

Here is the syntax for the `MERGE` clause for creating relationships:

[source.big,cypher]
----
MERGE (variable1:Label1 {nodeProperties1})-[:REL_TYPE]->
(variable2:Label2 {nodeProperties2})
RETURN variable
----

[.notes]
--
If there is an existing node with  _Label1_ and _nodeProperties1_  with the _:REL_TYPE_ relationship to an existing node with  _Label2_ and _nodeProperties2_ in the graph, no relationship is created. If the relationship does not exist, the relationship is created.
--

=== Example: Finding existing relationships

[.statement]
Here is an example. We currently have the _Person_ node with the _:ACTED_IN_ relationship, but we do not have this relationship with the _Actor_ node.

[source,cypher]
----
MATCH (p {name: 'Michael Caine'})-[*0..1]-(m)
RETURN p, m
----

ifndef::env-slides[]
Here is the result:
endif::[]

image::MichaelCainesAndOneRel.png[MichaelCainesAndOneRel,width=600,align=center]

=== Example: Using `MERGE` to create relationship

[.statement]
Here is code where we want to create the _:ACTED_IN_ relationship between _Michael Caine_ and the movie _Batman Begins_.

[source,cypher]
----
MATCH (p {name: 'Michael Caine'}),(m:Movie {title:'Batman Begins'})
MERGE (p)-[:ACTED_IN]->(m)
RETURN p,m
----

ifndef::env-slides[]
Here is the result of running this code:
endif::[]

image::MergeToCreateRel.png[MergeToCreateRel,width=600,align=center]

[.notes]
--
Since the relationship between the _Person_ node and the _Movie_ node already exists, it is not created.
The relationship between the _Actor_ node and the _Movie_ node is created with this merge.


[NOTE]
[.statement]
Although, you can leave out the direction of the relationship being created with the `MERGE`, in which case a left-to-right arrow will be assumed, a best practice is to always specify the direction of the relationship. However, if you have  bidirectional relationships and you want to avoid creating duplicate relationships, you [.underline]#must# leave off the arrow.
--

== Specifying creation behavior when merging

[.notes]
--
You can use the `MERGE` clause, along with `ON CREATE` to assign specific values to a node being created as a result of an attempt to merge.

Here is an example where create a new node, specifying property values for the new node:
--

[source,cypher]
----
MERGE (a:Person {name: 'Sir Michael Caine'})
ON CREATE SET a.birthPlace = 'London',
              a.born = 1934
RETURN a
----

[.notes]
--
We know that there are no existing _Sir Michael Caine_ _Person_ nodes.
When the `MERGE` executes, it will not find any matching nodes so it will create one and will execute the `ON CREATE` clause where we set the _birthplace_ and _born_ property values.

Here is the result of executing this code:
--

image::CreateSirMichaelCaine.png[CreateSirMichaelCaine,width=1000,align=center]

=== Example: Verifying the merge

[.statement]
Here is the code to display the nodes that have anything to do with _Michael Caine_.

[source,cypher]
----
MATCH (p)-[*0..1]-(m)
WHERE p.name CONTAINS 'Caine'
RETURN p, m
----

[.statement]
The most recently created node has the _name_ value of _Sir Michael Caine_.

image::AllMichaelCaines.png[AllMichaelCaines,width=600,align=center]

== Specifying update behavior when merging

[.notes]
--
You can also specify an `ON MATCH` clause during merge processing.
If the exact node is found, you can update its properties or labels. Here is an example:
--

[source,cypher]
----
MERGE (a:Person {name: 'Sir Michael Caine'})
ON CREATE SET a.born = 1934, 
              a.birthPlace = 'UK'
ON MATCH SET a.birthPlace = 'UK'
----

[.notes]
--
And here we see that only the existing node with the name, _Sir Michael Caine_ is updated with the new _birthPlace_.
Furthermore, no new node is created for _Sir Michael Caine_.
--

image::UpdateToUK.png[UpdateToUK,width=300,align=center]

== Using `MERGE` to create relationships

[.notes]
--
Using `MERGE` to create relationships is expensive and you should only do it when you need to ensure that a relationship is unique and you are [underline]#not# sure if it already exists.

In this example, we use the `MATCH` clause to find all _Person_ nodes that represent _Michael Caine_ and we find the movie, _Batman Begins_ that we want to connect to all of these nodes. We already have a connection between one of the _Person_ nodes and the _Movie_ node. We do not want this relationship to be duplicated. This is where we can use `MERGE` as follows:
--

[source,cypher]
----
MATCH (p:Person), (m:Movie)
WHERE m.title = 'Batman Begins' AND p.name ENDS WITH 'Caine'
MERGE (p)-[:ACTED_IN]->(m)
RETURN p, m
----

[.notes]
--
Here is the result of executing this Cypher statement. It went through all the nodes and added the relationship to the nodes that didn't already have the relationship.
--

image::MergeRelationships.png[MergeRelationships,width=1000,align=center]

[.notes]
--
You must be aware of the  behavior of the `MERGE` clause and how it will automatically create nodes and relationships.
`MERGE` tries to find a full pattern and if it doesn't find it, it creates that full pattern.
That's why in most cases you should first `MERGE` your nodes and then your relationship afterwards.
--

=== Use MERGE carefully


Only if you intentionally want to create a node within the context of another (like a month within a year) then a `MERGE` clause with one bound and one unbound node makes sense.

For example:

[source,cypher]
----
MATCH (fromDate:Date {year: 2018})
MERGE (toDate:Date {month: 'January'})-[:IN_YEAR]->(fromDate)
----

[.student-exercise]
== Exercise 12: Merging data in the graph

In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-intro-neo4j-exercises]

and follow the instructions for Exercise 12.

[NOTE]
This exercise has 16 steps.
Estimated time to complete: 45 minutes.

[.quiz]
== Check your understanding

=== Question 1

[.statement]
Given this `MERGE` clause, what is the most important thing you should make sure of?

[source,cypher]
----
MERGE (p:Person {name: 'Jane Doe'})
SET p.born = 1990
RETURN p
----

[.statement]
Select the correct answer.

[%interactive.answers]
- [ ] The _Person_ label exists in the graph.
- [ ] The _Person_ label does not exist in the graph.
- [x] The value for name is unique.
- [ ] The value for born is unique.

=== Question 2

[.statement]
Given this `MERGE` clause.
Suppose that the _p_ and _m_ nodes exist in the graph.
What does this code do?

[source,cypher]
----
MATCH (p {name: 'Jane Doe'}),(m:Movie {title:'The Good One'})
MERGE (p)-[:ACTED_IN]->(m)
----

[.statement]
Select the correct answers.

[%interactive.answers]
- [ ] If the _:ACTED_IN_ relationship exists, it deletes it and recreates it.
- [x] If the _:ACTED_IN_ relationship exists, it does nothing.
- [x] If the _:ACTED_IN_ relationship does not exist, it creates it.
- [ ] If the _:ACTED_IN_ relationship does not exist, it creates another one.

=== Question 3

[.statement]
Given this `MERGE` clause.
Suppose that the _p_ and _m_ nodes exist in the graph, but you do not know whether the relationship exists.
What are your options to process this `MERGE` clause?

[source,cypher]
----
MATCH (p {name: 'Jane Doe'}),(m:Movie {title:'The Good One'})
MERGE (p)-[rel:ACTED_IN]->(m)
SET rel.role=['role']
----

[.statement]
Select the correct answers.

[%interactive.answers]
- [x] Use the default behavior. The relationship will be created if it doesn't exist.
- [x] Specify `ON CREATE` to perform additional processing when the relationship is created.
- [x] Specify `ON MATCH` to perform additional processing when the relationship is not created.
- [ ] Specify `ON DELETE` to perform additional processing when the relationship is deleted.

[.summary]
== Summary

You should now be able to write Cypher statements to:
[square]
* Merge data in a graph by:
** Creating nodes.
** Creating relationships.
