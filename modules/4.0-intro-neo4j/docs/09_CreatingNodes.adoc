= Creating Nodes
:slug: creating-nodes
:doctype: book
:toc: left
:toclevels: 4
:imagesdir: ../images
:module-next-title: Creating Relationships

== About this module

[.notes]
--
Using an existing dataset, you have learned how to query the graph with both simple and complex patterns, how to control processing during a query, and how to control how results are returned.
Next, you will learn how to add data to a graph by creating nodes.

--

At the end of this module, you should be able to write Cypher statements to:
[square]
* Create a node.
* Add and remove node labels.
* Add and remove node properties.
* Update node properties.

== Syntax: Creating nodes

[.notes]
--
Recall that a node is an element of a graph representing a domain entity that has zero or more labels, properties, and relationships to or from other nodes in the graph. 

When you create a node, you can add it to the graph without connecting it to another node.
In virtually all cases, the nodes that you create in a graph will be connected to other nodes and you will learn about creating these connections (relationships) in the next lesson of this training.
--

Here is the simplified syntax for creating a node:

[source.big,cypher]
----
CREATE (optionalVariable optionalLabels {optionalProperties})
----

[.notes]
--
If you plan on referencing the newly created node, you must provide a variable. Whether you provide labels or properties at node creation time is optional. In most cases, you will want to provide some label and property values for a node when created. This will enable you to later retrieve the node. Provided you have a reference to the node (for example, using a `MATCH` clause), you can always add, update, or remove labels and properties at a later time.

Next, you will see some examples of creating a single node in Cypher:
--

=== Example #1

Add a node to the graph of type _Movie_ with the _title_ _Batman Begins_. This node can be retrieved using the title. A set of nodes with the label _Movie_ can also be retrieved which will contain this node:

[source,cypher]
----
CREATE (:Movie {title: 'Batman Begins'})
----

=== Example #2

Add a node with two labels to the graph of types _Movie_ and _Action_ with the _title_ _Batman Begins_. This node can be retrieved using the title. A set of nodes with the labels _Movie_ or _Action_ can also be retrieved which will contain this node:

[source,cypher]
----
CREATE (:Movie:Action {title: 'Batman Begins'})
----

=== Example #3

Add a node to the graph of types _Movie_ and _Action_ with the _title_ _Batman Begins_. This node can be retrieved using the title. A set of nodes with the labels _Movie_ or _Action_ can also be retrieved which will contain this node. The variable _m_ can be used for later processing after the `CREATE` clause:

[source,cypher]
----
CREATE (m:Movie:Action {title: 'Batman Begins'})
----

=== Example #4

Add a node to the graph of types _Movie_ and _Action_ with the _title_ _Batman Begins_. This node can be retrieved using the title. A set of nodes with the labels _Movie_ or _Action_ can also be retrieved which will contain this node. Here we return the title of the node:

[source,cypher]
----
CREATE (m:Movie:Action {title: ' Batman Begins'})
RETURN m.title
----

=== Result of creating node

[.notes]
--
Here is what you see in Neo4j Browser when you create a node for the movie, _Batman Begins_ where we use the variable, _m_ to return its _title_ property value:
--

[.is-half.left]
--

image::CreateBatmanBegins.png[CreateBatmanBegins,width=600,align=center]
--

[.notes]
--
Here is the node when we retrieve it. It is the only node in the graph with both a label of _Action_ so to retrieve it from the graph is straightforward where we only specify the node labels.
--

[is-half.right]
--

image::RetrieveBatmanBegins.png[RetrieveBatmanBegins,width=400,align=center]
--

[.notes]
--
When the graph engine creates a node, it automatically assigns a read-only, unique ID to the node.  
Here we see that the _id_ of the node is _171_. This is not a property of a node, but rather an internal value.

After you have created a node, you can add more properties or labels to it and most importantly, connect it to another node.
--

== Creating multiple nodes

[.notes]
--
You can create multiple nodes by simply separating the nodes specified with commas, or by specifying multiple `CREATE` statements.

Here is an example, where we create some _Person_ nodes that will represent some of the people associated with the movie _Batman Begins_:
--

[.is-two-thirds.left]
--
[source,cypher]
----
CREATE 
(:Person {name: 'Michael Caine', born: 1933}),
(:Person {name: 'Liam Neeson', born: 1952}),
(:Person {name: 'Katie Holmes', born: 1978}),
(:Person {name: 'Benjamin Melniker', born: 1913})
----
--

ifndef::backend-revealjs[]
Here is the result of running this Cypher statement:
endif::[]

[.is-one-third.right]
--
image::CreateMultiplePersonNodes.png[CreateMultiplePersonNodes,width=400,align=center]
--

[NOTE]
--
The graph engine will create a node with the same properties of a node that already exists. You can prevent this from happening in one of two ways:

* You can use `MERGE` rather than `CREATE` when creating the node.

* You can add constraints to your graph.
--

[.notes]
--
You will learn about merging data later in this course. Constraints are configured globally for a graph and are also covered later in this course.
--

== Syntax: Adding labels to a node

[.notes]
--
You may not know ahead of time what label or labels you want for a node when it is created. You add labels to a node using the `SET` clause.

Here is the simplified syntax for adding labels to a node:
--

[source.big,cypher]
----
SET x:Label         // adding one label to node referenced by the variable x
----

[source.big,cypher]
----
SET x:Label1:Label2	// adding two labels to node referenced by the variable x
----

If you attempt to add a label to a node for which the label already exists, the `SET` processing is ignored.

=== Example: Adding labels to a node

[.notes]
--
Here is an example where we add the _Fantasy_ label to the node that has a labels, _Movie_ and _Action_:
--

[source,cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Batman Begins'
SET m:Fantasy
RETURN labels(m)
----

[.notes]
--
Assuming that we have previously created the node for the movie, here is the result of running this Cypher statement:
--

image::SetFantasyLabel.png[SetFantasyLabel,width=800,align=center]

[.notes]
--
Notice here that we call the built-in function, `labels()` that returns the set of labels for the node.
--

== Syntax: Removing labels from a node

[.notes]
--
Perhaps your data model has changed or the underlying data for a node has changed so that the label for a node is no longer useful or valid. 

Here is the simplified syntax for removing a label from a node:
--

[source.big,cypher]
----
// remove the label from the node referenced by the variable x
REMOVE x:Label
----

[.notes]
--
Here is the simplified syntax for removing multiple labels from a node:
--

[source.big,cypher]
----
// remove the two labels from the node referenced by the variable x
REMOVE x:Label1, x:Label2
----

[.notes]
--
If you attempt to remove a label from a node for which the label does not exist, it is ignored.
--

== Example: Removing labels from a node

[.notes]
--
Here is an example where we remove the _Action_  and _Fantasy_ labels from the node that has the label, _Action_:
--

[source,cypher]
----
MATCH (m:Action)
REMOVE m:Action, m:Fantasy
RETURN labels(m)
----

[.statement]
Assuming that we have previously created the node for the movie, here is the result of running this Cypher statement:

image::RemoveActionLabel.png[RemoveActionLabel,width=700,align=center]

== Syntax: Adding properties to a node

[.notes]
--
After you have created a node and have a reference to the node, you can add properties to the node, again using the `SET` keyword. 

Here are simplified syntax examples for adding properties to a node referenced by the variable x:
--

[source.big,cypher]
----
SET x.propertyName = value
----

[source.big,cypher]
----
SET x.propertyName1 = value1	, x.propertyName2 = value2 
----

[source.big,cypher]
----
SET x = {propertyName1: value1, propertyName2: value2}
----

[source.big,cypher]
----
SET x += {propertyName1: value1, propertyName2: value2}
----

[.notes]
--
If the property does not exist, it is added to the node. If the property exists, its value is updated. If the value specified is `null`, the property is removed. 

Note that the type of data for a property is not enforced.  
That is, you can assign a string value to a property that was once a numeric value and visa versa.

When specify the JSON-style object for assignment (using `=`) of the property values for the node, the object must include all of the properties and their values for the node as the existing properties for the node are overwritten. However, if you specify `+=` when assigning to a property, the value at _valueX_ is updated if the _propertyNnameX_ exists for the node. If the _propertyNameX_ does not exist for the node, then the property is added to the node.
--

== Example: Adding properties to a node

[.notes]
--
Here is an example where we add the properties _released_  and _lengthInMinutes_ to the movie _Batman Begins_:
--

[source,cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Batman Begins'
SET m.released = 2005, m.lengthInMinutes = 140
RETURN m
----

[.statement]
Assuming that we have previously created the node for the movie, here is the result of running this Cypher statement:

image::AddReleasedMinutesProperties.png[AddReleasedMinutesProperties,width=800,align=center]

== Example 2: Adding properties to node

[.notes]
--
Here is another example where we set the property values to the movie node using the JSON-style object containing the property keys and values. Note that [.underline]#all# properties must be included in the object.
--

[source,cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Batman Begins'
SET  m = {title: 'Batman Begins', 
          released: 2005, 
          lengthInMinutes: 140, 
          videoFormat: 'DVD', 
          grossMillions: 206.5}
RETURN m
----

ifndef::backend-revealjs[]
Here is the result of running this Cypher statement:
endif::[]

image::SetPropertiesObject.png[SetPropertiesObject,width=800,align=center]

[.notes]
--
Note that when you add a property to a node for the first time in the graph, the property key is added to the graph.
So for example, in the previous example, we added the _videoFormat_ and _grossMillions_ property keys to the graph as they have never been used before for a node in the graph.
Once a property key is added to the graph, it is [.underline]#never# removed.
--

== Viewing property keys

[.is-one-third.left]
--
image::AllPropertyKeys1.png[AllPropertyKeys1,width=250,align=center]
--
[.notes]
--
When you examine the property keys in the database (by executing `CALL db.propertyKeys()`, you will see all property keys created for the graph, regardless of whether they are currently used for nodes and relationships.
--

[.is-two-thirds.right]
--
image::AllPropertyKeys2.png[AllPropertyKeys2,width=700,align=center]
--

== Retrieving properties of a node

[.notes]
--
In addition to querying the names of all properties, you can also return the properties of a specific node.
You can retrieve the properties of a node as a Cypher map:
--

[source, cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Batman Begins'
RETURN properties(m)
----

ifndef::backend-revealjs[]
Here is the result returned:
endif::[]

image::PropertyKeysBatmanBegins.png[PropertyKeysBatmanBegins,width=800,align=center]

== Updating properties - JSON-style

[.notes]
--
Here is an example where we use the JSON-style object to add the _awards_ property to the node and update the _grossMillions_ property:
--

[source,cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Batman Begins'
SET  m += { grossMillions: 300,
            awards: 66}
RETURN m
----

ifndef::backend-revealjs[]
Here is the result:
endif::[]

image::AddAndUpdateProperties.png[AddAndUpdateProperties,width=900,align=center]

== Syntax: Removing properties from a node

[.notes]
--
There are two ways that you can remove a property from a node. One way is to use the REMOVE keyword. The other way is to set the property's value to `null`.

Here are simplified syntax examples for removing properties from a node referenced by the variable x:
--

[source.big,cypher]
----
REMOVE x.propertyName
----

[source.big,cypher]
----
SET x.propertyName = null
----

== Example: Removing properties from a node

[.notes]
--
Suppose we determined that no other _Movie_ node in the graph has the properties, _videoFormat_ and _grossMillions_.
There is no restriction that nodes of the same type must have the same properties.
However, we have decided that we want to remove these properties from this node.
Here is example Cypher to remove this property from this _Batman Begins_ node:
--

[source,cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Batman Begins'
SET m.grossMillions = null
REMOVE m.videoFormat
RETURN m
----

[.statement]
Assuming that we have previously created the node for the movie with the these properties, here is the result of running this Cypher statement where we remove each property a different way. One way we remove the property using the `SET` clause to set the property to null. And in another way, we use the `REMOVE` clause.

image::RemoveProperties.png[RemoveProperties,width=800,align=center]

[.student-exercise]
== Exercise 9: Creating nodes

In the query edit pane of Neo4j Browser, execute the browser command: kbd:[:play 4.0-intro-neo4j-exercises]
and follow the instructions for Exercise 9.

[NOTE]
This exercise has 18 steps.
Estimated time to complete: 30 minutes.


[.quiz]
== Check your understanding

=== Question 1

[.statement]
What Cypher clauses can you use to create a node? 

[.statement]
Select the correct answers.

[%interactive.answers]
- [x] `CREATE`
- [ ] `CREATE NODE`
- [x] `MERGE`
- [ ] `ADD`

=== Question 2

[.statement]
Suppose that you have retrieved a node, _s_ with a property, _color_.
What Cypher clause do you add here to delete the _color_ property from this node?

[source,cypher]
----
MATCH (s:Shape {location: [20,30]})
???
RETURN s
----

[.statement]
Select the correct answers.

[%interactive.answers]
- [ ] `DELETE s.color`
- [x] `SET s.color=null`
- [x] `REMOVE s.color`
- [ ] `SET s.color=?`

=== Question 3

[.statement]
Suppose you have this _Person_ node in the graph: "name": "Joe Cool", "birthYear": 1985.
What properties have values after executing this code?

[source,cypher]
-----
MATCH (p:Person) WHERE p.name = 'Joe Cool'
SET  p += { salary: 130000,
            department: 'Engineering'}
REMOVE p.birthYear
-----

[.statement]
Select the correct answers.

[%interactive.answers]
- [x] name
- [ ] birthYear
- [x] salary
- [x] department

[.summary]
== Summary

You should now be able to write Cypher statements to:
[square]

* Create a node.
* Add and remove node labels.
* Add and remove node properties.
* Update node properties.

////
== Grade Quiz and Continue 

++++
<a class="next-section medium button" href="../part-7/">Continue to Module 7</a>
++++

ifdef::backend-html5[]

include::scripts-end.txt[]
++++
<script>
$( document ).ready(function() {
  Intercom('trackEvent','training-introv2-view-part6');
});
</script>
++++

endif::backend-html5[]
////
