= Working with Patterns in Queries
:slug: 03-querying40-working-with-patterns-in-queries
:doctype: book
:toc: left
:toclevels: 4
:imagesdir: ../images
:page-slug: {slug}
:page-layout: training
:page-quiz:
:page-module-duration-minutes: 30

== About this module

[.notes]
--
You have learned how to query nodes and relationships in a graph using simple patterns and also how to use the `WHERE` clause for filtering queries.
--

At the end of this module, you should be able to write Cypher statements to:
[square]
* Specify multiple `MATCH` patterns.
* Specify multiple `MATCH` clauses.
* Specify varying length paths.
* Return a subgraph.
* Specify `OPTIONAL` in a query.

== Traversal in a MATCH clause

[.statement]
Suppose we want to find all of the followers of people who reviewed the movie, _The Replacements_.

ifndef::env-slides[]
Here is the query to do this:
endif::[]

[source,Cypher,role=noplay]
----
MATCH (follower:Person)-[:FOLLOWS]->(reviewer:Person)-[:REVIEWED]->(m:Movie)
WHERE m.title = 'The Replacements'
RETURN follower.name, reviewer.name
----

ifndef::env-slides[]
Here is the result:
endif::[]

[.is-half.right-column]
--
image::TheReplacements.png[TheReplacements,width=1000,align=center]
--

[.notes]
--
Here is the traversal that the graph engine performed.
It first found the movie, _The Replacements_.
Then it found all _Person_ nodes that reviewed that movie, _Angela_, _Jessica_, and _James_.
Then it found all _Person_ nodes who follow the people who reviewed the movie, _Paul_, _Angela_, and _James_.
In all, six relationships were traversed.
--

[.is-half.left-column]
--
image::TheReplacementsTraversal.png[TheReplacementsTraversal,width=500,align=center]
--

== Specifying multiple patterns in a `MATCH`

[.notes]
--
Up until now, you have specified a single `MATCH` pattern in a query with filtering in a `WHERE` clause.
You can specify multiple patterns in a `MATCH` clause.

Suppose we want to write queries that focus on movies released in the year _2000_.
Here are the nodes and relationships for these movies:
--

image::Movies2000.png[Movies2000,width=700,align=center]

[.notes]
--
This `MATCH` clause includes a pattern  specified by two paths separated by a comma:
--

[.is-half.left-column]
--
[source,Cypher,role=noplay]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie), 
      (m)<-[:DIRECTED]-(d:Person)
WHERE m.released = 2000
RETURN a.name, m.title, d.name
----
--

[.notes]
--
It returns all _Person_ nodes for people who acted in these three movies and using that same movie node, _m_  it retrieves the _Person_ node who is the director for that movie, _m_.
--

ifndef::env-slides[]
Here is the result of executing this query:
endif::[]

[.is-half.right-column]
--
image::Movies2000ActorsDirectors.png[Movies2000ActorsDirectors,width=300,align=center]
--

[.notes]
--
It returns 15 rows, one for each actor with the associated movie title and name of the director for that particular movie.
When multiple patterns are specified in a `MATCH` clause, no relationship is traversed more than one time.
--

== Specifying a single pattern

However, a better way to write this same query would be:

[source,Cypher,role=noplay]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person)
WHERE m.released = 2000
RETURN a.name, m.title, d.name
----

[.notes]
--
There are, however, some queries where you will need to specify two or more patterns. 
Multiple patterns are used when a query is complex and cannot be satisfied with a single pattern. 
This is useful when you are looking for a specific node in the graph and want to connect it to a different node. 
You can learn about creating nodes and relationships in the course, _Creating Nodes and Relationships in Neo4j 4.x_.
--

=== Example: Using two patterns in a `MATCH`

[.notes]
--
Here are some examples of specifying two paths in a `MATCH` clause.

In the first example, we want the actors that worked with _Keanu Reeves_ to meet _Hugo Weaving_, who has worked with _Keanu Reeves_. 
Here we retrieve the actors who acted in the same movies as _Keanu Reeves_, but not when _Hugo Weaving_ acted in the same movie. 
To do this, we specify two paths for the `MATCH`:
--


[source,Cypher,role=noplay]
----
MATCH (keanu:Person)-[:ACTED_IN]->(movie:Movie)<-[:ACTED_IN]-(n:Person),
     (hugo:Person)
WHERE keanu.name='Keanu Reeves' AND
      hugo.name='Hugo Weaving'
AND NOT (hugo)-[:ACTED_IN]->(movie)
RETURN n.name
----


[.notes]
--
When you perform this type of query, you may see a warning in the query edit pane stating that the pattern represents a cartesian product and may require a lot of resources to perform the query. 
You should only perform these types of queries if you know the data well and the implications of doing the query.

If you click the warning symbol in the top left corner, it produces an explanation result pane.
--

[.is-half.left-column]
--
image::CartesianProductWarning.png[CartesianProductWarning,width=500,align=center]
--

ifndef::env-slides[]
Here is the result of executing this query:
endif::[]

[.is-half.right-column]
--
image::KeanuFriendsForHugo.png[KeanuFriendsForHugo,width=500,align=center]
--

[.notes]
--
The actors _Laurence Fishburne_, _Carrie-Anne Moss_, _Emil Eifrem_ (and of course _Hugo Weaving_ and _Keanu Reeves_) do not appear in the results list, because these actors were in the same movie (_The Matrix_) as _Huge Weaving_ and _Keanu Reeves_.
--

=== Example: Two patterns in a `MATCH` required

[.notes]
--
Here is another example where two patterns are necessary.

Suppose we want to retrieve the movies that _Meg Ryan_ acted in and their respective directors, as well as the other actors that acted in these movies. 
Here is the query to do this:
--

[source,Cypher,role=noplay]
----
MATCH (meg:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person),
      (other:Person)-[:ACTED_IN]->(m)
WHERE meg.name = 'Meg Ryan'
RETURN m.title as movie, d.name AS director , other.name AS `co-actors`
----

ifndef::env-slides[]
Here is the result returned:
endif::[]

image::MegsCoActors.png[MegsCoActors,width=400,align=center]

== Traversal with patterns

[.notes]
--
During a query, you want to minimize the number of paths traversed.
In some cases, however, you can only retrieve the nodes, relationships, or paths of interest using multiple patterns or even multiple `MATCH` clauses.

Here is an example query where multiple `MATCH` clauses are used:

--


[source,Cypher,role=noplay]
----
MATCH (valKilmer:Person)-[:ACTED_IN]->(m:Movie)
MATCH (actor:Person)-[:ACTED_IN]->(m)
WHERE valKilmer.name = 'Val Kilmer'
RETURN m.title as movie , actor.name
----


[.notes]
--
The first `MATCH` clause retrieves Val Kilmer pointing to the movie, _Top Gun_ using the _ACTED_IN_ relationship.
The anchor of this `MATCH` clause is the _Val Kilmer_ _Person_ node.
The second `MATCH` clause retrieves all _Person_ nodes that have the _ACTED_IN_ relationship with the movie, _Top Gun_.
The anchor of the `MATCH` clause is the _Top Gun_ _Movie_node.

When the query engine traverses the graph for these `MATCH` clauses, we see that the _ACTED_IN_ relationship is traversed twice.
--

[.is-half.left-column]
--
image::ValKilmerCoActorsMultipleMatchTraversal.png[ValKilmerCoActorsMultipleMatchTraversal,width=400,align=center]
--

ifndef::env-slides[]
Here is the result returned:
endif::[]

[.is-half.right-column]
--
image::ValKilmerCoActorsMultipleMatch.png[ValKilmerCoActorsMultipleMatch,width=600,align=center]
--

== Traversal: Multiple patterns in a `MATCH` clause

[.notes]
--
Here is the same example where multiple patterns are specified in a single `MATCH` clause:
--

[source,Cypher,role=noplay]
----
MATCH (valKilmer:Person)-[:ACTED_IN]->(m:Movie),
      (actor:Person)-[:ACTED_IN]->(m)
WHERE valKilmer.name = 'Val Kilmer'
RETURN m.title as movie , actor.name
----


[.notes]
--
The `MATCH` clause retrieves the _Val Kilmer_ node and uses the _ACTED_IN_ relationship to retrieve the _Top Gun_ node, then it uses the movie node to retrieve all actors.
With this scenario, the _ACTED_IN_ relationship is only traversed once.
We already know the _Person_ node for _Val Kilmer_ so we need not return it.
--

[.is-half.left-column]
--
image::ValKilmerCoActorsSingleMatchTraversal.png[ValKilmerCoActorsSingleMatchTraversal,width=400,align=center]
--

[.notes]
--
The result returned is smaller because it does not include the _Val Kilmer_ node.
--

[.is-half.right-column]
--
image::ValKilmerCoActorsSingleMatch.png[ValKilmerCoActorsSingleMatch,width=800,align=center]
--
[.notes]
--
A best practice is to traverse as few nodes as possible so in this example, using multiple `MATCH` patterns is best.
--

== Specifying varying length paths

[.notes]
--
Any graph that represents social networking, trees, or hierarchies will most likely have multiple paths of varying lengths. 
Think of the _connected_ relationship in _LinkedIn_ and how connections are made by people connected to more people.  
The _Movie_ database for this training does not have much depth of relationships, but it does have the _:FOLLOWS_ relationship that you learned about earlier:
--

[.is-one-third.left-column]
--
image::FollowsRelationships.png[FollowsRelationships,width=400,align=center]
--

[.notes]
--
You write a `MATCH` clause where  you want to find all of the followers of the followers of a _Person_ by specifying a numeric value for the number of hops in the path. 
Here is an example where we want to retrieve all _Person_ nodes that are exactly two hops away:
--

[.is-two-thirds.right-column]
--
[source,Cypher,role=noplay]
----
MATCH (follower:Person)-[:FOLLOWS*2]->(p:Person)
WHERE follower.name = 'Paul Blythe'
RETURN p.name
----

ifndef::env-slides[]
Here is the result returned:
endif::[]

image::TwoHopRelationship.png[TwoHopRelationship,width=700,align=center]
--

[.notes]
--
If we had specified `[:FOLLOWS*]` rather than `[:FOLLOWS*2]`, the query would return all _Person_ nodes that are in the `:FOLLOWS` path from _Paul Blythe_.
--

== Syntax: Varying length patterns - 1

[.notes]
--
Here are simplified syntax examples for how varying length patterns are specified in Cypher:
--

[.statement]
Retrieve [.underline]#all# paths of any length with the relationship, _:RELTYPE_ from _nodeA_ to _nodeB_ and beyond:


[source,syntax,role=nocopy noplay]
----
(nodeA)-[:RELTYPE*]->(nodeB)
----

[.statement]
Retrieve [.underline]#all# paths of any length with the relationship, _:RELTYPE_ from _nodeA_ to _nodeB_ or from _nodeB_ to _nodeA_ and beyond. This is usually a very expensive query so you should place limits on how many nodes are retrieved:

[source,syntax,role=nocopy noplay]
----
(nodeA)-[:RELTYPE*]-(nodeB)
----

== Syntax: Varying length patterns - 2

[.statement]
Retrieve the paths of length 3 with the relationship, _:RELTYPE_ from _nodeA_ to _nodeB_:

[source,syntax,role=nocopy noplay]
----
(node1)-[:RELTYPE*3]->(node2)
----

[.statement]
Retrieve the paths of lengths 1, 2, or 3 with the relationship, _:RELTYPE_ from _nodeA_ to _nodeB_, _nodeB_ to _nodeC_, as well as, _nodeC_ to _nodeD) (up to three hops):

[source,syntax,role=nocopy noplay]
----
(node1)-[:RELTYPE*1..3]->(node2)
----

== Finding the shortest path

[.notes]
--
A built-in function that you may find useful in a graph that has many ways of traversing the graph to get to the same node is the `shortestPath()` function. Using the shortest path between two nodes improves the performance of the query.

In this example, we want to discover a shortest path between the movies _The Matrix_ and _A Few Good Men_. In our `MATCH` clause, we set the variable _p_ to the result of calling `shortestPath()`, and then return _p_. In the call to `shortestPath()`, notice that we specify `*` for the relationship. This means any relationship; for the traversal.
--

[source,Cypher,role=noplay]
----
MATCH p = shortestPath((m1:Movie)-[*]-(m2:Movie))
WHERE m1.title = 'A Few Good Men' AND
      m2.title = 'The Matrix'
RETURN  p
----


[.notes]
--
When you specify this `MATCH` clause to use the `shortestPath()` function as shown here with an unbounded varying length, you will see this warning:
--

[.is-half.left-column]
--
image::ShortestPathWarning.png[shortestPathWarning,width=600,align=center]
--

[.notes]
--
You should heed the warning, especially for large graphs.
You can also read the _Graph Data Science_ documentation about the shortest path algorithm, which performs even better than the one that is build into Cypher.

Here is the result returned:
--

[.is-half.right-column]
--
image::ShortestPath1.png[shortestPath1,width=500,align=center]
--

[.notes]
--
Notice that the graph engine has traversed many types of relationships to get to the end node.

When you use `shortestPath()`, you can specify a upper limits for the shortest path. In addition, you should aim to provide the patterns for the from and to nodes that execute efficiently. For example, use labels and indexes.
--

== Returning a subgraph

[.notes]
--
In using `shortestPath()`, the return type is a path. A subgraph is essentially as set of paths derived from your `MATCH` clause.

For example, here is an example where we want a subgraph of all nodes connected to the movie, _The Replacements_:
--

[source,Cypher,role=noplay]
----
MATCH paths = (m:Movie)-[rel]-(p:Person)
WHERE m.title = 'The Replacements'
RETURN paths
----

[.notes]
--
If in Neo4j Browser where have unset *Connect result nodes*, the result is visualized as a graph because the query has returned a set of paths which are a subgraph.

Here is the result of this query:
--

[.is-one-third.left-column]
--
image::Subgraph1.png[Subgraph1,width=800,align=center]
--

[.notes]
--
If you view the result as text, you will see that it is simply a set of rows where a movie is connected to a person:
--

[.is-two-thirds.right-column]
--
image::Subgraph2.png[Subgraph2,width=800,align=center]
--

[.notes]
--
Some actor relationships have data for the roles property or summary property of the relationship. Note that in this text, the name of the relationship is not shown, but is it in the graph visualization.
Later in this course, you will learn more about working with lists, which is what this data represents.

The APOC library is very useful if you want to query the graph to obtain subgraphs.
--

== Specifying optional pattern matching

[.notes]
--
`OPTIONAL MATCH` matches patterns with your graph, just like `MATCH` does.
The difference is that if no matches are found, `OPTIONAL MATCH` will use nulls for missing parts of the pattern.
`OPTIONAL MATCH` could be considered the Cypher equivalent of the outer join in SQL.
--

[.is-one-third.left-column]
--
[.statement]
Here is a subgraph of our movies graph with all people named _James_ and their relationships:

image::TheJames.png[TheJames,width=800,align=center]
--

[.notes]
--
Here is an example where we query the graph for all people whose name starts with _James_.  
The `OPTIONAL MATCH` is specified to include people who have reviewed movies: 
--

[.is-two-thirds.right-column]
--
[source,Cypher,role=noplay]
----
MATCH (p:Person)
WHERE p.name STARTS WITH 'James'
OPTIONAL MATCH (p)-[r:REVIEWED]->(m:Movie)
RETURN p.name, type(r), m.title
----

ifndef::env-slides[]
Here is the result returned:
endif::[]

image::OptionalMatch.png[OptionalMatch,width=800,align=center]
--

[.notes]
--
Notice that for all rows that do not have the _:REVIEWED_ relationship, a _null_ value is returned for the movie part of the query, as well as the relationship.
--

[.student-exercise]
== Exercise 5: Working with patterns in queries

In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-intro-neo4j-exercises]

and follow the instructions for Exercise 5.

[NOTE]
This exercise has 6 steps.
Estimated time to complete: 30 minutes.

[.quiz]
== Check your understanding

=== Question 1

[.statement]
Given this Cypher query:

[source,Cypher,role=noplay]
----
MATCH (follower:Person)-[:FOLLOWS]->(reviewer:Person)-[:REVIEWED]->(m:Movie)
WHERE m.title = 'The Replacements' RETURN follower.name, reviewer.name
----

[.statement]
What is the first node that is retrieved by the query engine?

[.statement]
Select the correct answer.

[%interactive.answers]
- [ ] The first _Person_ node with a _FOLLOWS_ relationship
- [ ] The first _Person_ node with a _REVIEWED_ relationship
- [x] The _Movie_ node for the movie, The Replacements
- [ ] The first _Movie_ node in the alphabetical list of movies in the graph

=== Question 2

[.statement]
We want a query that returns a list of people who acted in movies released later than 2005 and for those movies, also return title and released year of the movie, as well as the name of the writer.
How can you correct this query?

[source,Cypher,role=noplay]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
      (m)<-[:WROTE]-(w:Person)
WHERE m.released > 2005
RETURN a.name, m.title, m.released, w.name
----

[.statement]
Select the correct answer.

[%interactive.answers]
- [ ] The second line should be: `(m2:Movie)<-[:WROTE]-(w:Person)`.
- [x] Add a comma after the first pattern in the `MATCH` clause.
- [ ] The second line should be: `(m2:Movie)<-[:WROTE]-(a)`.
- [ ] Add a `MATCH` clause at the beginning of the second line.

=== Question 3

[.statement]
Suppose you have a graph of _Person_ nodes representing a social network graph.
A _Person_ node can have a _IS_FRIENDS_WITH_ relationship with any other _Person_ node.
Like in Facebook, there can be a long path of connections between people.
What Cypher `MATCH` clause would you use to find all people in this graph that are two to four hops away from each other?

[.statement]
Select the correct answer.

[%interactive.answers]
- [x] `MATCH (p:Person)-[:IS_FRIENDS_WITH*2..4]->(p2:Person)`
- [ ] `MATCH (p:Person)-[:IS_FRIENDS_WITH*2-4]->(p2:Person)`
- [ ] `MATCH (p:Person)-[:IS_FRIENDS_WITH,2-4]->(p2:Person)`
- [ ] `MATCH (p:Person)-[:IS_FRIENDS_WITH,2,4]->(p2:Person)`

[.summary]
== Summary

You should now be able to write Cypher statements to:
[square]
* Specify multiple `MATCH` patterns.
* Specify multiple `MATCH` clauses.
* Specify varying length paths.
* Return a subgraph.
* Specify `OPTIONAL` in a query.
