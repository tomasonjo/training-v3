= Exercise 4
:icons: font

== Exercise 4:Louvain Modularity (Preparations)

The database you start with should contain all of the data you loaded in the setup for this course.

This is what you should see when you click the database icon image:database-icon.png[].

image::LoadedDatabase.png[LoadedDatabase,width=150]

If you do not see this in your Neo4j Browser, you will need to perform the setup steps again.

== Exercise 4:Louvain Modularity (Overview)

The Louvain Modularity algorithm finds clusters by comparing community density as it assigns nodes to different groups.
You can think of this as a “what if” analysis to try out various grouping with the goal of eventually reaching a global optimum.

It also reveals a hierarchy of communities at different scales, which is useful for understanding the structure of a network at different levels of granularity.

In this exercise, you will perform the following with the European Roads dataset:

* *Exercise 4.1*: Run the Louvain Modularity algorithm to add a community property to each node.
* *Exercise 4.2*: Examine the results of executing the algorithm.
* *Exercise 4.3*: Run the Louvain Modularity algorithm to discover a hierarchy of communities within the graph.
* *Exercise 4.4*: Examine the results of executing the algorithm.
* *Exercise 4.5*: View the partitioning of the data.
* *Exercise 4.6*: View finer-grained partition data.

Go to the next page to start this exercise.

== Exercise 4.1: Run the Louvain Modularity algorithm to add a community property to each node. (Instructions)

Write Cypher code to perform the Louvain Modularity algorithm on the *Place* nodes and the *EROAD* relationship using these guidelines:

* The analysis will be done using BOTH directions.
* The algorithm will write a property named *community* to each node with the computed value.
* The weight property name is *weight*.
* `YIELD` the following results: nodes, communityCount, iterations, loadMillis, computeMillis, writeMillis.

*Hint*: You will call `algo.louvain`.

== Exercise 4.1: Run the Louvain Modularity algorithm to add a community property to each node. (Solution)

Write Cypher code to perform the Louvain Modularity algorithm on the *Place* nodes and the *EROAD* relationship using these guidelines:

* The analysis will be done using BOTH directions.
* The algorithm will write a property named *community* to each node with the computed value.
* The weight property name is *weight*.
* `YIELD` the following results: nodes, communityCount, iterations, loadMillis, computeMillis, writeMillis.

*Hint*: You will call `algo.louvain`.

Here is the solution code:

[source, cypher]
----
CALL algo.louvain('Place', 'EROAD',
{writeProperty:'community', weightProperty: 'weight', direction: 'Both' })
YIELD nodes, communityCount, iterations, loadMillis, computeMillis, writeMillis
----

The results returned should look like this:

[.thumb]
image::EX5.1.png[EX5.1,width=600]

== Exercise 4.2: Verify the results of executing the algorithm. (Instructions)

Write a query to return all *community* values in the graph containing *Place* nodes.
For each distinct community value, return the list of places.

== Exercise 4.2: Verify the results of executing the algorithm. (Solution)

Write a query to return all *community* values in the graph containing *Place* nodes.
For each distinct community value, return the list of places.

Here is the solution code:

[source, cypher]
----
MATCH (node:Place)
RETURN DISTINCT node.community, collect(node.name) AS places
ORDER BY node.community DESC
----

The results returned should look like this:

[.thumb]
image::EX5.2.png[EX5.2,width=600]

As you can see, the algorithm created 25 different communities.

== Exercise 4.3: Run the Louvain Modularity algorithm to discover a hierarchy of communities within the graph. (Instructions)

Write Cypher code to perform the Louvain Modularity algorithm on the *Place* nodes and the *EROAD* relationship using these guidelines:

* The analysis will be done using BOTH directions.
* Specify that intermediate communities are to be analized.
* The algorithm will write a property named *communities* to each node with the computed value for intermediate communities.
* The weight property name is *weight*.
* `YIELD` the following results: nodes, communityCount, iterations, loadMillis, computeMillis, writeMillis.

*Hint*: You will call `algo.louvain`.

== Exercise 4.3: Run the Louvain Modularity algorithm to discover a hierarchy of communities within the graph. (Solution)

Write Cypher code to perform the Louvain Modularity algorithm on the *Place* nodes and the *EROAD* relationship using these guidelines:

* The analysis will be done using BOTH directions.
* Specify that intermediate communities are to be analized.
* The algorithm will write a property named *communities* to each node with the computed value for intermediate communities.
* The weight property name is *weight*.
* `YIELD` the following results: nodes, communityCount, iterations, loadMillis, computeMillis, writeMillis.

*Hint*: You will call `algo.louvain`.

Here is the solution code:

[source, cypher]
----
CALL algo.louvain('Place', 'EROAD',
{includeIntermediateCommunities: true, intermediateCommunitiesWriteProperty:'communities', weightProperty: 'weight', direction: 'Both' })
YIELD nodes, communityCount, iterations, loadMillis, computeMillis, writeMillis
----

The results returned should look like this:

[.thumb]
image::EX5.3.png[EX5.3,width=600]

== Exercise 4.4: Verify the results of executing the algorithm. (Instructions)

Write a query to return all *communities* values in the graph containing *Place* nodes.
For each distinct communities value, return the list of places.

== Exercise 4.4: Verify the results of executing the algorithm. (Solution)

Write a query to return all *communities* values in the graph containing *Place* nodes.
For each distinct communities value, return the list of places.

Here is the solution code:

[source, cypher]
----
MATCH (node:Place)
RETURN DISTINCT node.communities, collect(node.name) AS places
ORDER BY node.communities
----

The results returned should look like this:

[.thumb]
image::EX5.4.png[EX5.4,width=600]

== Exercise 4.5: View the partitioning of the data. (Instructions/Solution)

You can then query the graph to find which nodes are in each partition.
Let’s start with the final (and most coarse grained partition):

Execute this code:

[source, cypher]
----
MATCH (place:Place)
RETURN place.communities[-1] AS community, collect(place.name) AS places
ORDER BY size(places) DESC
----

The results returned should look like this:

[.thumb]
image::EX5.5A.png[EX5.5A,width=600]

If you want to find one of the intermediate partitions you can execute this code:

[source, cypher]
----
MATCH (place:Place)
RETURN place.communities[0] AS community, collect(place.name) AS places
ORDER BY size(places) DESC
----

The results returned should look like this:

[.thumb]
image::EX5.5B.png[EX5.5B,width=400]

== Exercise 4.6: View finer-grained partition data. (Instructions/Solution)

It may be easier to see how the algorithm works if we look at the intermediate partitions for a single place.
Let’s explore the clusters that London was assigned to.

You can find the first (and finest-grained) partition by running the following query:

[source, cypher]
----
WITH 0 AS index
MATCH (home:Place {name: "London"})
MATCH (place:Place) WHERE place.communities[index] = home.communities[index]
RETURN place.communities[index] AS community, collect(place.name) AS places
ORDER BY size(places) DESC
----

The results returned should look like this:

[.thumb]
image::EX5.6.png[EX5.6,width=700]

If we want to find the coarser-grained partitions we can change the first line of the query to look at different indexes in the array.
Try changing it to 1, 2, and 3 to see the clusters that London ends up in.

Try looking up the partitions for another place. e.g. Berlin, Paris, Amsterdam.

== Exercise 4: Louvain Modularity: Taking it further

. Try using the stream version of the algorithm.
. Try different configuration values.

== Exercise 4: Louvain Modularity (Summary)

In this exercise, you gained some experience with writing Cypher to implement the Louvain Modularity algorithm using the European Roads dataset.
The Louvain Modularity algorithm finds clusters by comparing community density as it assigns nodes to different groups.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/05.html'>Continue to Exercise 5</a>]
endif::[]
