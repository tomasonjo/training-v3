= Graph Catalog
:icons: font

== Graph Catalog (Preparations)

The database you start with should contain all of the data you loaded in the setup for this course.

This is what you should see when you click the database icon image:database-icon.png[].

image::DataLoaded.png[DataLoaded,width=150]

If you do not see this in your Neo4j Browser, you will need to perform the setup  and load data steps again.

== Graph Catalog (Overview)

The graph catalog is a concept within the GDSL that allows managing multiple graph projections by name.
Using its name, a created graph can be used many times in the analytical workflow.

In this exercise, you will gain some experience with writing Cypher to create a graph in the Graph Catalog.

* *Part 1*: Project multiple relationship orientations in a single named graph
* *Part 2*: Execute the Degree Centrality algorithm on a named graph
* *Part 3*: Execute the Degree Centrality algorithm on a named graph and filter relationship types
* *Part 4*: Drop a projected graph from Graph Catalog
* *Part 5*: Project European Roads graph to Graph Catalog with native projection.
* *Part 6*: Project a subgraph of European Roads dataset to Graph Catalog with cypher projection.

Go to the next page to start this exercise.

== Part 1: Project multiple relationship orientations in a single named graph. (Instructions/Solution)

In the Guided Exercise, you have executed the Degree centrality separately for the *NATURAL* and *REVERSE* orientation of *HELPS* relationship type.
Here, you will project both the *NATURAL* and *REVERSE* orientation of the *HELPS* relationship type in a single named graph.

* The created graph should be named *PersonGraph*.
* Project all nodes labeled *Person*.
* Project all relationships with a type *HELPS* and include both the *NATURAL* and *REVERSE* orientations.

*Hint*: You will call `gds.graph.create`.

[source, cypher]
----
CALL gds.graph.create('PersonGraph', 'Person',
  {
    HELPS_REVERSE: {
      type: 'HELPS',
      orientation: 'REVERSE'
   },
   HELPS_NATURAL: {
     type:'HELPS',
     orientation:'NATURAL'
   }
  })
----

== Part 2: Execute the Degree Centrality algorithm on a named graph (Instructions/Solution)

You will execute the Degree Centrality algorithm on the *Person* named graph without any additional configuration options.
As you do not specify which relationship types should the algorithm consider, it will use all available relationship types as an input.

*Hint*: You will call `gds.alpha.degree.stream`.

[source, cypher]
----
CALL gds.alpha.degree.stream('PersonGraph')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name as name, score
ORDER BY score DESC
----

Note that if we consider both the *NATURAL* and *REVERSE* orientation of a relationship type as in input to a graph algorithm, we will get identical results as if we projected it *UNDIRECTED*. You can confirm this by running the *UNDIRECTED* orientation of the *HELPS* relationship in NEuler.

This is what you should see in the result pane:

image::UndirectedDegree.png[NativeProjection,width=500]

== Part 3: Execute the Degree Centrality algorithm on a named graph and filter relationship types (Instructions/Solution)

The advantage of using a named graph with multiple relationship types is that you can filter which relationships should the algorithm consider at the execution time with the *relationshipTypes* parameter.
You will execute the Degree Centrality algorithm on the *Person* named graph and consider only *HELPS_NATURAL* relationships.

*Hint*: You will call `gds.alpha.degree.stream` and include the `relationshipTypes` parameter.

[source, cypher]
----
CALL gds.alpha.degree.stream('PersonGraph', 
  {relationshipTypes:['HELPS_NATURAL']})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name as name, score
ORDER BY score DESC
----

This is what you should see in the result pane:

image::DirectedDegree.png[NativeProjection,width=500]


== Part 4: Drop a projected graph from Graph Catalog (Instructions)

After you have completed your analysis on the Person graph, it is advisable to release the projected graph from Graph Catalog to free up memory.

*Hint*: You will call `gds.graph.drop`.

== Part 4: Drop a projected graph from Graph Catalog (Solution)

After you have completed your analysis on the Person graph, it is advisable to release the projected graph from Graph Catalog to free up memory.

*Hint*: You will call `gds.graph.drop`.

[source, cypher]
----
CALL gds.graph.drop('PersonGraph')
----

== Part 5: Project European Roads graph to Graph Catalog with native projection. (Instructions)

You will reuse the European Road network throughout the course to familiarize yourself with various graph algorithms.
For this reason, you will now project the European Roads graph as a named graph in Graph Catalog.

Write Cypher code to project the European Roads graph to Graph Catalog using these guidelines:

* The created graph should be named *roads*.
* Project all nodes labeled *Place*.
* Project all relationships with a type *EROAD*.
* Relationships should use the *UNDIRECTED* orientation.
* Include *inverse_distance* property as a relationship property.
* Set the default value for *inverse_distance* relationship property to be 1.0.

*Hint*: You will call `gds.graph.create`.

== Part 6: Project European Roads graph to Graph Catalog with native projection. (Solution)

Write Cypher code to project the European Roads graph to Graph Catalog using these guidelines:

* The created graph should be named *roads*.
* Project all nodes labeled *Place*.
* Project all relationships with a type *EROAD*.
* Relationships should use the *UNDIRECTED* orientation.
* Include *inverse_distance* property as a relationship property.
* Set the default value for *inverse_distance* relationship property to be 1.0.

*Hint*: You will call `gds.graph.create`.

[source, cypher]
----
CALL gds.graph.create('roads','Place',
    {EROAD:{
        type:"EROAD",
        orientation:"UNDIRECTED",
        properties:{
            inverse_distance:{
                property:"inverse_distance",
                defaultValue:1.0
            }
        }
    }
})
----

This is what you should see in the result pane:

image::NativeProjection.png[NativeProjection,width=500]

== Part 5: Project a subgraph of European Roads dataset to Graph Catalog with cypher projection. (Instructions/Solution)

In some parts of this course, you will analyze the DACH region subgraph of the European Roads network.
The native projection is not expressive enough to allow you to project *Place* nodes with only *D*,*A*, or *CH* country codes. 
Due to this limitation of native projection, you will use cypher projection to describe the DACH subregiond of the European Roads network.

Write Cypher code to project the European Roads graph to Graph Catalog using these guidelines:

* The created graph should be named *dach-region*.
* Project all nodes labeled *Place* that have one of the following country codes: *D*, *A*, or *CH*
* Project all relationships with a type *EROAD* and treat them as *UNDIRECTED*.
* Include *inverse_distance* property as a relationship property.
* Set the default value for *inverse_distance* relationship property to be 1.0.

*Hint*: You will call `gds.graph.create.cypher`.

[source, cypher]
----
CALL gds.graph.create.cypher('dach-region',
   'MATCH (p:Place) 
    WHERE p.countryCode in ["D", "A", "CH"]
    RETURN id(p) as id',
   'MATCH (s:Place)-[r:EROAD]-(t:Place)
    RETURN id(s) as source,id(t) as target, coalesce(r.inverse_distance,1.0) as inverse_distance'
    , {validateRelationships:false})
----

This is what you should see in the result pane:

image::Subgraph.png[Subgraph,width=500]

== Graph Catalog: Taking it further

. Try projecting the Game of Thrones network.
. Try using different filters with cypher projection

== Graph Catalog (Summary)

In this exercise, you gained some experience with writing Cypher code to store the projected graph in Graph Catalog.
You have also seen an example of how to filter relationship types at algorithm execution time.
The Graph Catalog enables you to reuse the same projected graph in the analytical workflow.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/MemoryRequirements.html'>Continue to Exercise: Memory Requirements</a>]
endif::[]
