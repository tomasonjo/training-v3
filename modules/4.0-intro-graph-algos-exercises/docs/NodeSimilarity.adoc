= Node Similarity
:icons: font

== Node Similarity (Preparations)

The database you start with should contain all of the data you loaded in the setup for this course.

This is what you should see when you click the database icon image:database-icon.png[].

image::DataLoaded.png[DataLoaded,width=150]

If you do not see this in your Neo4j Browser, you will need to perform the setup  and *Load Data* steps.


== Node Similarity (Overview)

Similarity algorithms are useful for analyzing datasets to determine how alike nodes are based upon their properties and graph topology.

In this exercise, you will execute Node Similarity algorithm on the Recipes dataset:

* *Part 1*: View the Recipes dataset schema.
* *Part 2*: Find the most common ingredients.
* *Part 3*: Project the Recipes graph to graph catalog with native projection.
* *Part 4*: Run the Node Similarity algorithm to evaluate the distribution of the Jaccard index similarity score.
* *Part 5*: Run the Node Similarity algorithm to add a relationship between a pair of similar recipes.
* *Part 6*: Inspect the results of the algorithm.
* *Part 7*: Recommend similar recipes


Go to the next page to start this exercise.

== Part 1: View the Recipes dataset schema. (Instructions)

The Recipes dataset contains *Recipe* nodes that are connected by the *CONTAINS_INGREDIENT* relationship to *Ingredient* nodes.
Write a query to view some of the *RECIPE* nodes and *CONTAINS_INGREDIENT* relationships in this dataset.

== Part 1: View the Recipes dataset schema. (Solution)

Write a query to view some of the *RECIPE* nodes and *CONTAINS_INGREDIENT* relationships in this dataset.

Here is the solution code:

[source, cypher]
----
MATCH p=(:Recipe)-[:CONTAINS_INGREDIENT]-(:Ingredient)
RETURN p LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EXNS.1.png[EXNS.1,width=300]

{nbsp} +

Recipes contain one or more ingredient.
Some ingredients are used in many recipes.

== Part 2: Find the most common ingredients. (Instructions)

Next, you will identify the most common ingredients.

Write a query to find the top five common ingredients used in the recipes.

*Hint*: Match all *Ingredient* nodes and count their *CONTAINS_INGREDIENT* relationships.

== Part 2: Find the most common ingredients. (Solution)

Write a query to find the top five common ingredients used in the recipes.

*Hint*: Match all *Ingredient* nodes and count their *CONTAINS_INGREDIENT* relationships.

Here is the solution code:

[source, cypher]
----
MATCH (i:Ingredient)
RETURN i.name as ingredient,
       size((i)<-[:CONTAINS_INGREDIENT]-()) as count
ORDER BY count DESC LIMIT 5
----

The results returned should look like this:

[.thumb]
image::EXNS.2.png[EXNS.2,width=300]

{nbsp} +

The most common ingredient used is sugar, followed by egg and flour.

== Part 3: Project the Recipes graph to graph catalog with native projection. (Instructions)

The Recipes dataset is not yet projected as a named graph in the Graph Catalog.
Here you will project a named graph that consists of nodes with labels *Recipe* and *Ingredient*, connected by the *CONTAINS_INGREDIENT* relationships.

Write Cypher code to project the Recipes graph to Graph Catalog using these guidelines:

* The created graph should be named *recipes*.
* Project all *Recipe* and *Ingredient* nodes.
* Project all relationships with a type *CONTAINS_INGREDIENT*.

*Hint*: You will call `gds.graph.create`.

== Part 3: Project the Recipes graph to graph catalog with native projection. (Solution)

Write Cypher code to project the Recipes graph to Graph Catalog using these guidelines:

* The created graph should be named *recipes*.
* Project all *Recipe* and *Ingredient* nodes.
* Project all relationships with a type *CONTAINS_INGREDIENT*.

*Hint*: You will call `gds.graph.create`.

Here is the solution code:

[source, cypher]
----
CALL gds.graph.create('recipes',
  ['Ingredient','Recipe'],'CONTAINS_INGREDIENT')
----

You should see this result:

[.thumb]
image::EXNS.3.png[EXNS.3,width=600]

== Part 4: Run the Node Similarity algorithm to evaluate the distribution of the Jaccard index similarity score. (Instructions)

Before we infer a similarity network between recipes, we will evaluate the similarity score distribution.
Examining the similarity score distribution will help us fine-tune the *topK* and *similarityCutoff* parameters of the Node Similarity algorithm in the next step.
This is probably the most crucial step of the Node Similarity algorithm as the *topK* and *similarityCutoff* parameters directly influence how sparse or dense the inferred similarity network will be.

Write Cypher code to examine the similarity score distribution of the Recipes dataset following these guidelines:

* The algorithm should use the projected graph *recipes*, which is stored in the graph catalog.
* Use the `stats` mode of the Node Similarity algorithm.
* `YIELD` the following result: nodesCompared,similarityDistribution.

*Hint*: You will call `gds.nodeSimilarity.stats`.

== Part 4:  Run the Node Similarity algorithm to evaluate the distribution of the Jaccard index similarity score. (Solution)

Write Cypher code to examine the similarity score distribution of the Recipes dataset following these guidelines:

* The algorithm should use the projected graph *recipes*, which is stored in the graph catalog.
* Use the `stats` mode of the Node Similarity algorithm.
* `YIELD` the following result: nodesCompared,similarityDistribution.

*Hint*: You will call `gds.nodeSimilarity.stats`.

Here is the solution code:

[source, cypher]
----
CALL gds.nodeSimilarity.stats('recipes')
YIELD nodesCompared, similarityDistribution
----

The results returned should look like this:

[.thumb]
image::EXNS.4.png[EXNS.4,width=300]

{nbsp} +

The Jaccard similarity score ranges from 0 to 1, where 1 means that the two recipes use the exact same ingredients.
On average, a pair of recipes share around 35% of ingredients.
Judging by the *p90* score, only 10% of recipe pairs share more than 55% of ingredients.
The recipes are not very similar, so you will have to use a lower *similarityCutoff* parameter to not infer too sparse similarity network.

== Part 5: Run the Node Similarity algorithm to add a relationship between a pair of similar recipes. (Instructions)

After the similarity score distribution examination, you can go ahead and write the similarity network between recipes back to Neo4j.

Write Cypher code to execute the Node Similarity algorithm on the Recipes dataset using these guidelines:

* The algorithm should use the projected graph *recipes*, which is stored in the graph catalog.
* Use the `write` mode of the Node Similarity algorithm.
* The algorithm will write a relationship with a type *SIMILAR* between a pair of similar recipes.
* The algorithm will write a property named *score* to each relationship with the computed value.
* Specify a similarity cutoff threshold of 0.45.
* Specify the *topK* parameter of 5.
* `YIELD` the following result: nodesCompared, relationshipsWritten.

*Hint*: You will call `gds.nodeSimilarity.write`.

== Part 5: Run the Node Similarity algorithm to add a relationship between a pair of similar recipes. (Solution)

Write Cypher code to execute the Node Similarity algorithm on the Recipes dataset using these guidelines:

* The algorithm should use the projected graph *recipes*, which is stored in the graph catalog.
* Use the `write` mode of the Node Similarity algorithm.
* The algorithm will write a relationship with a type *SIMILAR* between a pair of similar recipes.
* The algorithm will write a property named *score* to each relationship with the computed value.
* Specify a similarity cutoff threshold of 0.45.
* Specify the *topK* parameter of 5.
* `YIELD` the following result: nodesCompared, relationshipsWritten.

*Hint*: You will call `gds.nodeSimilarity.write`.

Here is the solution code:

[source, cypher]
----
CALL gds.nodeSimilarity.write('recipes',{
  writeRelationshipType:'SIMILAR',
  writeProperty:'score',
  similarityCutoff:0.45,
  topK:5
  })
YIELD nodesCompared, relationshipsWritten
----

The results returned should look like this:

[.thumb]
image::EXNS.5.png[EXNS.4,width=300]

{nbsp} +

The algorithm has written 3000 similarity relationships between 2178 nodes.
On average, a single recipe is deemed similar to 1.4 other recipes.
Even with the low *similarityCutoff* parameter, the inferred similarity network is still relatively sparse.

== Part 6: Inspect the results of the algorithm. (Instructions)

The similarity network contains *Recipe* nodes that are connected by the *SIMILAR* relationship.
Write a query to view some of the nodes and relationships in this network.

== Part 6: Inspect the results of the algorithm. (Solution)

The similarity network contains *Recipe* nodes that are connected by the *SIMILAR* relationship.
Write a query to view some of the nodes and relationships in this network.

Here is the solution code:

[source, cypher]
----
MATCH p=(:Recipe)-[:SIMILAR]-(:Recipe)
RETURN p LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EXNS.6.png[EXNS.6,width=300]

{nbsp} +

For example, Banana cue recipe is similar to Camote cue recipe.

== Part 7: Recommend similar recipes. (Instructions)

The inferred similarity network between recipes can be used to recommend similar recipes in your application.

Write Cypher code to recommend the three most similar recipes to *Sourdough* recipe.

== Part 7: Recommend similar recipes. (Solution)

Write Cypher code to recommend the three most similar recipes to *Sourdough* recipe.

Here is the solution code:

[source, cypher]
----
MATCH (r:Recipe)-[s:SIMILAR]->(recommendation)
WHERE r.name = 'Sourdough'
RETURN recommendation.name as recommendation, s.score as score
ORDER BY score DESC LIMIT 3
----

The results returned should look something like this:

[.thumb]
image::EXNS.7.png[EXNS.7,width=300]

{nbsp} +

If we want to try baking something similar to Sourdough, we could try Flatbread, Kitcha, or Sabaayad. 

== Node Similarity: Taking it further

* Change the similarity cutoff and topK parameters to see how it affects the results.

== Node Similarity (Summary)

Similarity algorithms are useful for analyzing datasets to determine how alike nodes are based upon their properties and graph topology.

In this exercise, you gained experience with Node Similarity algorithm on the Recipes dataset.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/PracticalApplication.html'>Continue to Exercise: Practical application</a>]
endif::[]
