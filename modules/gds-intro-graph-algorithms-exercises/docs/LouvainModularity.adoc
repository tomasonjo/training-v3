= Louvain Modularity
:icons: font

== Louvain Modularity (Preparations)

The database you start with should contain all of the data you loaded in the setup for this course.

This is what you should see when you click the database icon image:database-icon.png[].

image::LoadedDatabase.png[LoadedDatabase,width=150]

{nbsp} +

If you do not see this in your Neo4j Browser, you will need to perform the setup steps again.

== Louvain Modularity (Graph Catalog)

The projected graph *roads* should be stored in the GDS graph catalog.

This is what you should see when you execute the following query:

[source, cypher]
----
CALL gds.graph.list()
YIELD graphName, nodeCount, relationshipCount
----

image::LoadedRoadsGraph.png[LoadedDatabase]

If you do not see this in your Neo4j Browser, you will need to perform the graph catalog steps again.

== Louvain Modularity (Overview)

The Louvain Modularity algorithm finds clusters by comparing community density as it assigns nodes to different groups.
You can think of this as a “what if” analysis to try out various grouping with the goal of eventually reaching a global optimum.

It also reveals a hierarchy of communities at different scales, which is useful for understanding the structure of a network at different levels of granularity.

In this exercise, you will perform the following with the European Roads dataset:

* *Part 1*: Run the Louvain Modularity algorithm to add a community property to each node.
* *Part 2*: Examine the results of executing the algorithm.
* *Part 3*: Run the Louvain Modularity algorithm to discover a hierarchy of communities within the graph.
* *Part 4*: Examine the results of executing the algorithm.
* *Part 5*: View the partitioning of the data.
* *Part 6*: View finer-grained partition data.

Go to the next page to start this exercise.

== Part 1: Run the Louvain Modularity algorithm to add a community property to each node. (Instructions)

Write Cypher code to perform the Louvain Modularity algorithm on the European road network using these guidelines:

* The analysis will be done on the projected in-memory graph *roads*.
* The algorithm will write a property named *community_louvain* to each node with the computed value.
* The weight property name is *inverse_distance*.
* `YIELD` the following results: modularity, ranLevels, communityCount.

*Hint*: You will call `gds.louvain.write`.

== Part 1: Run the Louvain Modularity algorithm to add a community property to each node. (Solution)

Write Cypher code to perform the Louvain Modularity algorithm on the European road network using these guidelines:

* The analysis will be done on the projected in-memory graph *roads*.
* The algorithm will write a property named *community_louvain* to each node with the computed value.
* The weight property name is *inverse_distance*.
* `YIELD` the following results: modularity, ranLevels, communityCount.

*Hint*: You will call `gds.louvain.write`.

Here is the solution code:

[source, cypher]
----
CALL gds.louvain.write('roads',{
    writeProperty:'community_louvain', 
    relationshipWeightProperty: 'inverse_distance'})
YIELD modularity, ranLevels, communityCount
----

The results returned should look like this:

[.thumb]
image::EX5.1.png[EX5.1,width=400]

== Part 2: Verify the results of executing the algorithm. (Instructions)

Write a query to return all *community_louvain* values in the graph containing *Place* nodes.
For each distinct community value, return the list of places and the size of community.

== Part 2: Verify the results of executing the algorithm. (Solution)

Write a query to return all *community_louvain* values in the graph containing *Place* nodes.
For each distinct community value, return the list of places and the size of community.

Here is the solution code:

[source, cypher]
----
MATCH (node:Place)
RETURN node.community_louvain as communityId,
       count(*) as communitySize,
       collect(node.name) AS places
ORDER BY communitySize DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EX5.2.png[EX5.2,width=400]

{nbsp} +

As you can see, the algorithm found 25 different communities.

== Part 3: Run the Louvain Modularity algorithm to discover a hierarchy of communities within the graph. (Instructions)

Write Cypher code to perform the Louvain Modularity algorithm on the European road network using these guidelines:

* The analysis will be done on the projected in-memory graph *roads*.
* Specify that intermediate communities are to be analyzed.
* The algorithm will write a property named *communities_louvain* to each node with the computed value for intermediate communities.
* The weight property name is *inverse_distance*.
* `YIELD` the following results: modularity, ranLevels, communityCount.

*Hint*: You will call `gds.louvain.write`.

== Part 3: Run the Louvain Modularity algorithm to discover a hierarchy of communities within the graph. (Solution)

Write Cypher code to perform the Louvain Modularity algorithm on the European road network using these guidelines:

* The analysis will be done on the projected in-memory graph *roads*.
* Specify that intermediate communities are to be analyzed.
* The algorithm will write a property named *communities_louvain* to each node with the computed value for intermediate communities.
* The weight property name is *inverse_distance*.
* `YIELD` the following results: modularity, ranLevels, communityCount.

*Hint*: You will call `gds.louvain.write`.

Here is the solution code:

[source, cypher]
----
CALL gds.louvain.write('roads',{
    writeProperty:'communities_louvain', 
    relationshipWeightProperty: 'inverse_distance',
    includeIntermediateCommunities: true})
YIELD modularity, ranLevels, communityCount

----

The results returned should look like this:

[.thumb]
image::EX5.3.png[EX5.3,width=400]

== Part 4: Verify the results of executing the algorithm. (Instructions)

Write a query to return all *communities_louvain* values in the graph containing *Place* nodes.
For each distinct communities value, return the list of places and the community size.

== Part 4: Verify the results of executing the algorithm. (Solution)

Write a query to return all *communities_louvain* values in the graph containing *Place* nodes.
For each distinct communities value, return the list of places and the community size.

Here is the solution code:

[source, cypher]
----
MATCH (node:Place)
RETURN node.communities_louvain as communities,
       count(*) as communitiesSize,
       collect(node.name) AS places
ORDER BY communitiesSize DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EX5.4.png[EX5.4,width=400]

== Part 5: View the partitioning of the data. (Instructions/Solution)

You can then query the graph to find which nodes are in each partition.
Let’s start with the final (and most coarse grained partition):

The results will be the same as before

Execute this code:

[source, cypher]
----
MATCH (place:Place)
RETURN place.communities_louvain[-1] AS community,
       count(*) as communitiesSize,
       collect(place.name) AS places
ORDER BY communitiesSize DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::{guides}/img/EX5.5A.png[EX5.5A,width=400]

{nbsp} +

If you want to find one of the intermediate partitions you can execute this code:

[source, cypher]
----
MATCH (place:Place)
RETURN place.communities_louvain[0] AS community,
       count(*) as communitiesSize,
       collect(place.name) AS places
ORDER BY communitiesSize DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::{guides}/img/EX5.5B.png[EX5.5B,width=400]

== Part 6: View finer-grained partition data. (Instructions/Solution)

It may be easier to see how the algorithm works if we look at the intermediate partitions for a single place.
Let’s explore the clusters that London was assigned to.

You can find the first (and finest-grained) partition by running the following query:

[source, cypher]
----
UNWIND range(0,4) as level
MATCH (home:Place {name: "London"})
MATCH (place:Place) WHERE place.communities_louvain[level] = home.communities_louvain[level]
RETURN level,
       place.communities_louvain[level] AS community, 
       count(*) as communitiesSize,
       collect(place.name) AS places
----

The results returned should look like this:

[.thumb]
image::EX5.6.png[EX5.6,width=400]

{nbsp} +

If we want to find the coarser-grained partitions we can change the first line of the query to look at different indexes in the array.
Try changing it to 1, 2, and 3 to see the clusters that London ends up in.

Try looking up the partitions for another place. e.g. Berlin, Paris, Amsterdam.

== Louvain Modularity: Taking it further

. Try using the stream version of the algorithm.
. Try different configuration values.

== Louvain Modularity (Summary)

In this exercise, you gained some experience with writing Cypher to implement the Louvain Modularity algorithm using the European Roads dataset.
The Louvain Modularity algorithm finds clusters by comparing community density as it assigns nodes to different groups.

