= Louvain Modularity
:icons: font

== Louvain Modularity (Preparations)

The database you start with should contain all of the data you loaded in the setup for this course.

This is what you should see when you click the database icon image:database-icon.png[].

image::LoadedDatabase.png[LoadedDatabase,width=150]

{nbsp} +

If you do not see this in your Neo4j Browser, you will need to perform the setup steps again.

== Louvain Modularity (Graph Catalog)

The projected graphs *roads* and *dach-region* should be created and stored in the GDS graph catalog.

This is what you should see when you execute the following query:

[source, cypher]
----
CALL gds.graph.list()
YIELD graphName, nodeCount, relationshipCount
----

image::LoadedRoadsGraph.png[LoadedDatabase]

{nbsp} +

If you do not see this in your Neo4j Browser, you will need to perform the graph catalog steps again.

== Louvain Modularity (Overview)

The Louvain Modularity algorithm finds clusters by comparing community density as it assigns nodes to different groups.
You can think of this as a “what if” analysis to try out various grouping with the goal of eventually reaching a global optimum.

It also reveals a hierarchy of communities at different scales, which is useful for understanding the structure of a network at different levels of granularity.

In this exercise, you will perform the following with the European Roads dataset:

* *Part 1*: Run the Louvain Modularity algorithm to add a community property to each node.
* *Part 2*: Inspect the results of the algorithm.
* *Part 3*: Run the Louvain Modularity algorithm to discover the hierarchical structure of communities.
* *Part 4*: Inspect the hierarchical results of the algorithm.
* *Part 5*: View the communities on different hierarchical levels.
* *Part 6*: Examine the communities at each level for a single place.

Go to the next page to start this exercise.

== Part 1: Run the Louvain Modularity algorithm to add a community property to each node. (Instructions)

Write Cypher code to perform the weighted Louvain Modularity algorithm on the European road network using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* The algorithm will write a property named *community_louvain* to each node with the computed value.
* The relationship weight property name is *inverse_distance*.
* `YIELD` the following results: modularity, ranLevels, communityCount.

*Hint*: You will call `gds.louvain.write`.

== Part 1: Run the Louvain Modularity algorithm to add a community property to each node. (Solution)

Write Cypher code to perform the weighted Louvain Modularity algorithm on the European road network using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* The algorithm will write a property named *community_louvain* to each node with the computed value.
* The relationship weight property name is *inverse_distance*.
* `YIELD` the following results: modularity, ranLevels, communityCount.

*Hint*: You will call `gds.louvain.write`.

Here is the solution code:

[source, cypher]
----
CALL gds.louvain.write('roads',{
    writeProperty:'community_louvain', 
    relationshipWeightProperty: 'inverse_distance'})
YIELD modularity, ranLevels, communityCount
----

The results returned should look like this:

[.thumb]
image::EXLM.1.png[EX5.1,width=400]

{nbsp} +

Algorithm found three hierarchical levels of communities with a total of 24 communities on the last level.

== Part 2: Inspect the results of the algorithm. (Instructions)

Write a query to return all *community_louvain* values in the graph containing *Place* nodes.
For each distinct communities value, return the list of places and the community size.

* Order the results by community size descending.

== Part 2: Inspect the results of the algorithm. (Solution)

Write a query to return all *community_louvain* values in the graph containing *Place* nodes.
For each distinct communities value, return the list of places and the community size.

* Order the results by community size descending.
* Limit the top ten results

Here is the solution code:

[source, cypher]
----
MATCH (place:Place)
RETURN place.community_louvain AS communityId,
       count(*) AS communitySize,
       collect(place.name) AS places
ORDER BY communitySize DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EXLM.2.png[EXLM.2,width=400]

{nbsp} +

The largest community has 75 members.
It contains places from Belgium, Netherlands, France, and Germany.

== Part 3: Run the Louvain Modularity algorithm to discover a hierarchy of communities within the graph. (Instructions)

Write Cypher code to perform the Louvain Modularity algorithm on the European road network using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* Specify that intermediate communities are to be analyzed.
* The algorithm will write a property named *communities_louvain* to each node with the computed value for intermediate communities.
* The relationship weight property name is *inverse_distance*.
* `YIELD` the following results: modularity, ranLevels, communityCount.

*Hint*: You will call `gds.louvain.write`.

== Part 3: Run the Louvain Modularity algorithm to discover the hierarchal structure of communities. (Solution)

Write Cypher code to perform the Louvain Modularity algorithm on the European road network using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* Specify that intermediate communities are to be analyzed.
* The algorithm will write a property named *communities_louvain* to each node with the computed value for intermediate communities.
* The relationship weight property name is *inverse_distance*.
* `YIELD` the following results: modularity, ranLevels, communityCount.

*Hint*: You will call `gds.louvain.write`.

Here is the solution code:

[source, cypher]
----
CALL gds.louvain.write('roads',{
    writeProperty:'communities_louvain', 
    relationshipWeightProperty: 'inverse_distance',
    includeIntermediateCommunities: true})
YIELD modularity, ranLevels, communityCount
----

The results returned should look like this:

[.thumb]
image::EXLM.1.png[EXLM.1,width=400]

== Part 4: Inspect the hierarchical results of the algorithm. (Instructions)

Write a query to return all *communities_louvain* values in the graph containing *Place* nodes.
For each distinct communities value, return the list of places and the community size.

== Part 4: Inspect the hierarchical results of the algorithm. (Solution)

Write a query to return all *communities_louvain* values in the graph containing *Place* nodes.
For each distinct communities value, return the list of places and the community size.

Here is the solution code:

[source, cypher]
----
MATCH (place:Place)
RETURN place.communities_louvain AS communities,
       count(*) AS communitiesSize,
       collect(place.name) AS places
ORDER BY communitiesSize DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EXLM.4.png[EX5.4,width=400]

{nbsp} +

Antwerpen, Gent, Bruxelles, and six other places share the same community through all the hierarchical levels.

== Part 5: View the communities on different hierarchical levels. (Instructions/Solution)

You can then query the graph to find which communities form at each hierarchical level.
Let’s start with the final level (and most coarse-grained communities).

Execute this code:

[source, cypher]
----
MATCH (place:Place)
RETURN place.communities_louvain[-1] AS community,
       count(*) as communitiesSize,
       collect(place.name) AS places
ORDER BY communitiesSize DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EXLM.2.png[EXLM.2,width=400]

{nbsp} +

The results are identical to before when we ran the Louvain Modularity algorithm in Part 2.
The algorithm returns the last hierarchical level by default if we use the following parameter: `includeIntermediateCommunities:false`.

{nbsp} +

If you want to find one of the intermediate communities, you can execute this code:

[source, cypher]
----
MATCH (place:Place)
RETURN place.communities_louvain[0] AS community,
       count(*) as communitiesSize,
       collect(place.name) AS places
ORDER BY communitiesSize DESC
LIMIT 10
----

[.thumb]
image::EXLM.5.png[EXLM.5,width=400]

{nbsp} +

Communities on the first hierarchical level are the most fine-grained (smallest).

== Part 6: Examine the communities at each level for a single place. (Instructions/Solution)

It may be easier to see how the algorithm progresses through levels if we look at all the intermediate communities for a single place.

You can examine the communities on each hiearchical level for London by running the following query:

[source, cypher]
----
UNWIND range(0,2) as level
MATCH (home:Place {name: "London"})
MATCH (place:Place) WHERE place.communities_louvain[level] = home.communities_louvain[level]
RETURN level,
       place.communities_louvain[level] AS community, 
       count(*) as communitiesSize,
       collect(place.name) AS places
----

The results returned should look like this:

[.thumb]
image::EXLM.6.png[EXLM.6,width=400]

{nbsp} +

The first level community is relatively small and contains only London, Colchester, and Harwich.
On the next level, it already constains 22 members, and grows to 50 members on the third and final level.

Try looking up the communities for another place. e.g. Berlin, Paris, Amsterdam.

== Louvain Modularity: Taking it further

. Try using the stream version of the algorithm.
. Try different configuration values.

== Louvain Modularity (Summary)

In this exercise, you gained some experience writing Cypher to implement the Louvain Modularity algorithm using the European Roads dataset.
The Louvain Modularity algorithm finds clusters by comparing community density as it assigns nodes to different groups.

