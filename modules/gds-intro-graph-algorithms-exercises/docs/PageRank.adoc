= PageRank
:icons: font

== PageRank (Preparations)

The database you start with should contain all of the data you loaded in the setup for this course.

This is what you should see when you click the database icon image:database-icon.png[].

image::LoadedDatabase.png[LoadedDatabase,width=150]

{nbsp} +

If you do not see this in your Neo4j Browser, you will need to perform the setup steps again.

== PageRank (Graph Catalog)

The projected graph *roads* should be created and stored in the GDS graph catalog.

This is what you should see when you execute the following query:

[source, cypher]
----
CALL gds.graph.list()
YIELD graphName, nodeCount, relationshipCount
----

{nbsp} +

image::LoadedRoadsGraph.png[LoadedDatabase]

If you do not see this in your Neo4j Browser, you will need to perform the graph catalog steps again.

== PageRank (Overview)

PageRank measures the transitive influence or connectivity of nodes.
It can be computed by iteratively distributing one node’s rank (originally based on degree) over its neighbors.

In this exercise, you will execute PageRank on the European Roads dataset:

* *Part 1*: Perform the PageRank algorithm.
* *Part 2*: Perform the weighted variant of the PageRank algorithm.
* *Part 2*: Perform the Personalized PageRank algorithm.

Go to the next page to start this exercise.

== Part 1: Perform the PageRank algorithm. (Instructions)

Write Cypher code to perform PageRank analysis using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* Specify a maximum of 20 iterations.
* Use the `gds.util.asNode()` function to fetch the node from the nodeId value and return its name.
* Order the results by PageRank score descending.
* Limit the top ten results.


*Hint*: Call `gds.pageRank.stream`.

== Part 1: Perform the PageRank algorithm. (Solution)

Write Cypher code to perform PageRank analysis using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* Specify a maximum of 20 iterations.
* Use the `gds.util.asNode()` function to fetch the node from the nodeId value and return its name.
* Order the results by PageRank score descending.
* Limit the top ten results.

*Hint*: Call `gds.pageRank.stream`.

Here is the solution code:

[source, cypher]
----
CALL gds.pageRank.stream('roads', 
    {maxIterations:20})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS place, score
ORDER BY score DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EXPR.1.png[EXPR.1,width=400]

{nbsp} +

Here we see that Berlin, Budapest, and Praha are the important places in the networks judging by the PageRank centrality score.

== Perform the weighted variant of the PageRank algorithm. (Instructions)

Write Cypher code to perform the weighted variant of the PageRank analysis using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* Specify a maximum of 20 iterations.
* The relationship weight property name is *inverse_distance*.
* Use the `gds.util.asNode()` function to fetch the node from the nodeId value and return its name.
* Order the results by PageRank score descending.


*Hint*: Call `gds.pageRank.stream`.

== Perform the weighted variant of the PageRank algorithm. (Solution)

Write Cypher code to perform the weighted variant of the PageRank analysis using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* Specify a maximum of 20 iterations.
* Use the `gds.util.asNode()` function to fetch the node from the nodeId value and return its name.
* The relationship weight property name is *inverse_distance*.
* Order the results by PageRank score descending.


*Hint*: Call `gds.pageRank.stream`.

[source, cypher]
----
CALL gds.pageRank.stream('roads',{
    maxIterations:20, 
    relationshipWeightProperty:'inverse_distance'})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS place, score
ORDER BY score DESC
LIMIT 10
----

The results returned should look like this:

[.thumb]
image::EXPR.2.png[EXPR.2,width=400]

{nbsp} +

Here we see that with the weighted variant of PageRank, the most important places are Basel, Berlin, and Praha.

== Perform the Personalized PageRank algorithm. (Instructions/Solution)

Write Cypher code to perform the Personalized PageRank analysis using these guidelines:

* The algorithm should use the projected graph *roads*, which is stored in the graph catalog.
* Add the *sourceNodes* parameter.
* Specify *Madrid* as the source node and exclude it from results.

*Hint*: Call `gds.pageRank.stream`.

[source, cypher]
----
MATCH (p:Place)
WHERE p.name = 'Madrid'
CALL gds.pageRank.stream('roads', {
    sourceNodes:[p]})
YIELD nodeId, score
WHERE nodeId <> id(p)
RETURN gds.util.asNode(nodeId).name AS place, score
ORDER BY score DESC
LIMIT 10
----
[.thumb]
image::EXPR.3.png[EXPR.3,width=400]

{nbsp} +

We expect that the neighbouring places of Madrid will have a higher rank.

== PageRank: Taking it further

. Change the iterations and dampening factor to see how it affects the results.
. Change the sourceNodes parameter to see how it affects the results.
. Try using the non-stream version of the algorithm.

== PageRank (Summary)

PageRank measures the transitive influence or connectivity of nodes.
It can be computed by iteratively distributing one node’s rank (originally based on degree) over its neighbors.

In this exercise, you analyzed PageRank for the European Roads dataset.

