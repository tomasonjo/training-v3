= Applied Graph Data Science for Web Applications

== Applied Graph Data Science for Web Applications

Welcome to the Neo4j Applied Graph Data Science for Web Applications Training Course!

In this guide we're going to get familiar with the Yelp dataset.
Let's get started!

== Prerequisites

The database you start with should contain all of the data you loaded in the setup for this course.

This is what you should see when you click the database icon image:database-icon.png[].

image::DataLoaded.png[DataLoaded,width=150]

If you do not see this in your Neo4j Browser, you will need to perform the setup and load the Yelp dump file again.

== Graph Model

This is the graph model that we'll be working with:

image::yelp.svg[]

== Cypher Refresher

Cypher is a graph query language that we'll be using in this tutorial.
Let's do some exploratory analysis to get a feel for the data.

== Cypher Refresher: Cities

We'll start by seeing which cities have the most reviews:

[source,Cypher]
----
MATCH (c:City)
RETURN c.name, size((c)<-[:IN_CITY]-(:Business)<-[:REVIEWS]-(:Review)) AS numberOfReviews
ORDER BY numberOfReviews DESC
LIMIT 10;
----

The Yelp Public dataset contains data from a sampling of cities, but we'll contain our analysis to businesses in the Phoenix area.

== Cypher Refresher: Most Reviewed Restaurants

We can find the most reviewed restaurants in Phoenix by executing the following query:

[source,Cypher]
----
MATCH (b:Business)-[:IN_CITY]->(:City {name: "Phoenix"})
WHERE (b)-[:IN_CATEGORY]->(:Category {name: "Restaurants"})
RETURN b.id, b.name,
       [(b)-[:IN_CATEGORY]->(category) | category.name] AS categories,
       size((b)<-[:REVIEWS]-()) AS reviews
ORDER BY reviews DESC
LIMIT 10;
----

== Cypher Refresher: Interesting People

And who are some interesting people with the most reviews?

[source,Cypher]
----
MATCH (u:User)-[:WROTE]->()-[:REVIEWS]-(business:Business)
WHERE (business:Business)-[:IN_CITY]->(:City {name: "Phoenix"})

WITH u, count(*) AS totalReviews
ORDER BY totalReviews DESC
LIMIT 1000
MATCH (u)-[:WROTE]->()-[:REVIEWS]->()-[:IN_CATEGORY]->(cat)

WITH u, totalReviews, cat, count(*) AS count ORDER BY u, count DESC
WHERE count > 5
RETURN u.id, u.name, totalReviews, collect(cat.name)[..5] AS categories
ORDER BY totalReviews DESC;
----

Let's store some of those interesting users for later.
Run the following query to store them in the `userIds` parameter:

[source,Cypher]
----
:param userIds => ["FiKaWcORiTyKi4I7pJp3tQ", "d_TBs6J3twMy9GChqUEXkg", "NwPnvTALYMNZgdiwHHkuAQ"]
----

== Cypher Refresher: Interesting People

What places have these users been reviewing?

[source,Cypher]
----
MATCH (u:User)-[:WROTE]->()-[:REVIEWS]->(business:Business)-[:IN_CATEGORY]->(category)
WHERE u.id IN $userIds AND (business)-[:IN_CITY]->(:City {name: "Phoenix"})
WITH u, category, count(*) AS count
ORDER BY u, count DESC
RETURN u {.*} AS user, collect({category: category.name, count: count})[..5] AS reviews;
----

Now that we're familiar with the data, let's go back to the Graph Academy Page and learn about Graph Data Science and the application that we're going to build.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/02.html'>Continue to Exercise 2: Category Hierarchy</a>]
endif::[]
