= Exercise 5
:icons: font

== Exercise 5: Reducing Cardinality with `LIMIT`, `DISTINCT` (Preparations)

Before you begin this exercise, you should have this environment:

. Have a database that has access to the APOC library.

. If you have not populated the database yet, execute this Cypher code to populate the database:

[source,cypher]
----
include::scripts/AfterExercise4.cypher[]
----

This is what you should see in the left panel for the database:

[.thumb]
image::InitialDatabase.png[InitialDatabase.,width=300]

[start=3]
. If using Neo4j Desktop, modify the virtual memory and pagecache settings have been adjusted to be (server restart required:
[square]
* dbms.memory.heap.initial_size=4G
* dbms.memory.heap.max_size=4G
* dbms.memory.pagecache.size=1G

[start=4]
. If using Neo4j Aura, confirm that your settings are at least as large by running this:

[source,cypher]
----
CALL dbms.listConfig() YIELD name, value
WHERE name STARTS WITH 'dbms.memory'
RETURN name, value
----

[start=5]
. Warm up the Page cache (`CALL apoc.warmup.run(true,true,true)`)

You are now ready to start this exercise.

== Exercise 5: Reducing Cardinality with `LIMIT`, `DISTINCT` (Overview)

In this exercise, you will tune queries to further limit the number of rows processed.

In this exercise, you will:

* *Exercise 5.1*: Move `LIMIT` up in the query.
* *Exercise 5.2*: Tune a query.
* *Exercise 5.3*: Improve a query that returns unrelated data.

Go to the next page to start this exercise.

== Exercise 5.1: Move `LIMIT` up in the query (Instructions)

Here is a query that retrieves movies and its list of actors. We only want 10 of them.

[source, cypher]
----
PROFILE
MATCH (m:Movie)<-[:ACTED_IN]-(a)
WITH m, collect(a.name) AS Actors
RETURN m.title as Title, Actors LIMIT 10
----

. Execute this query (twice) and note how many rows are processed.

*IMPORTANT*: For all query tuning measurements, you must [underline]#always# run the query twice. The first time ensures that the compiled query is in the Query Cache. The second time ensures that the reported metrics for the execution plan do not include the cost of compilation.

[start=2]
. Rewrite this query to reduce the the number of rows processed.

*NOTE*: Remember to run the query twice to ensure no compilation time is included in the metrics the second time it is run.

== Exercise 5.1: Move `LIMIT` up in the query (Solution)

Here is a query that retrieves movies and its list of actors. We only want 10 of them.

[source, cypher]
----
PROFILE
MATCH (m:Movie)<-[:ACTED_IN]-(a)
WITH m, collect(a.name) AS Actors
RETURN m.title as Title, Actors LIMIT 10
----

. Execute this query (twice) and note how many rows are processed.

*IMPORTANT*: For all query tuning measurements, you must [underline]#always# run the query twice. The first time ensures that the compiled query is in the Query Cache. The second time ensures that the reported metrics for the execution plan do not include the cost of compilation.

This is what you should see for the execution plan:

[.thumb]
image::EX5.1.png[EX5.1,width=500]

[start=2]
. Rewrite this query to reduce the the number of rows processed.

*NOTE*: Remember to run the query twice to ensure no compilation time is included in the metrics the second time it is run.

The improved query should be:

[source, cypher]
----
PROFILE
MATCH (m:Movie)
WITH m LIMIT 10
MATCH (m)<-[:ACTED_IN]-(a)
WITH m, collect(a.name) AS Actors
RETURN m.title as Title, Actors
----
The result should be:

[.thumb]
image::EX5.1A.png[EX5.1A,width=500]

== Exercise 5.2: Tune a query (Instructions)

. Execute this Cypher code to return the names of directors of movies of each type of genre:

[source, cypher]
----
PROFILE
MATCH (d)-[:DIRECTED]-()-[:IS_GENRE]-(g)
RETURN   g.name, collect(d.name) AS Directors, count(d) as NumDirectors
----

What do you notice in the execution plan?

If you examine the collections returned, you will notice that there is duplication of the names of directors. We do not want that. Also notice that the query does an AllNodesScan which is not good.

[start=2]
. Rewrite this query to return collections with no duplication. Also make changes to it to reduce the number of rows in the execution plan.

== Exercise 5.2: Tune a query (Solution)

. Execute this Cypher code to return the names of directors of movies of each type of genre:

[source, cypher]
----
PROFILE
MATCH (d)-[:DIRECTED]-()-[:IS_GENRE]-(g)
RETURN   g.name, collect(d.name) AS Directors, count(d) as NumDirectors
----

What do you notice in the execution plan?

Here is what the execution plan looks like:

[.thumb]
image::EX5.2.png[EX5.2,width=500]

If you examine the collections returned, you will notice that there is duplication of the names of directors. We do not want that. Also notice that the query does an AllNodesScan which is not good.

[start=2]
. Rewrite this query to return collections with no duplication. Also make changes to it to reduce the number of rows in the execution plan.

Here is the revised query:

[source, cypher]
----
PROFILE
MATCH (d)-[:DIRECTED]-()-[:IS_GENRE]-(g:Genre)
WITH DISTINCT d, g.name AS GenreName
RETURN  GenreName, collect(d.name) AS Directors, count(d) as NumDirectors
----

We specify `DISTINCT` to elminate duplicate nodes. We also specify the _Genre_ label to eliminate the _AllNodesScan_.

Here is the new execution plan:

[.thumb]
image::EX5.2A.png[EX5.2A,width=500]

We have reduced the cardinality in the execution plan.

== Exercise 5.3: Improve a query that returns unrelated data (Instructions)

. Execute this Cypher code to retrieve the titles of all movies  released before 1950 and people born before 1920.

[source, cypher]
----
PROFILE MATCH (p:Person), (m:Movie)
WHERE  p.born < $bornYear AND m.releaseYear < $releaseYear
RETURN p, m
----

The $bornYear parameter should be set to  1920 and the $releaseYear parameter is set to 1950.

[start=2]
. What is wrong with this query? Modify this query to optimize it.

== Exercise 5.3: Improve a query that returns unrelated data   (Solution)

. Execute this Cypher code to retrieve the titles of all movies released before 1920 and people born before 1920.

[source, cypher]
----
:params {bornYear: 1920, releaseYear: 1950}
----

[source, cypher]
----
PROFILE
MATCH (p:Person), (m:Movie)
WHERE p.born < $bornYear AND m.releaseYear < $releaseYear
RETURN collect(DISTINCT p.name), collect(DISTINCT m.title)
----

The $bornYear parameter should be set to  1920 and the $releaseYear parameter is set to 1950.

The result of executing this query should be the following where we see a cartesian product:

[.thumb]
image::EX5.3.png[EX5.3,width=500]

[start=2]
. What is wrong with this query? Modify this query to optimize it.

Here is the solution code:

[source, cypher]
----
PROFILE
CALL {
MATCH (m:Movie) WHERE m.releaseYear < $releaseYear
RETURN collect(m) AS movies
}
CALL {
MATCH (a:Actor) WHERE a.born < $bornYear
RETURN collect(DISTINCT a) AS actors }
RETURN movies, actors
----

The result of executing this code should be:

[.thumb]
image::EX5.3A.png[EX5.3A,width=500]

== Exercise 5: Reducing Cardinality with `LIMIT`, `DISTINCT` (Summary)

In this exercise, you modified queries to perform better by further limiting the number of rows processed.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/06.html'>Continue to Exercise 6</a>]
endif::[]
